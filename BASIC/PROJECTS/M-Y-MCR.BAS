10 COLOR 7,0:CLS:LOCATE 10,27:PRINT "’ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ∏":LOCATE 11,27:PRINT "≥.çâêÖöêÑ öÄâòó çÖö Éí èöéÑ ÄêÄ≥":LOCATE 12,27:PRINT "‘ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕæ"
20 CLEAR:DEFINT A-Z:KL$="00000000":KEY OFF:DIM MEMO$(255),OP(255),LENG(255),MEMORY(8195),LIS(255),PA(15),BAN$(15),R$(11):GOTO 50
30 REM -------------- BREAK ---------
40 T$=INKEY$:IF T$="B" THEN GOSUB 300:LOCATE LY,2:PRINT SPC(40):X=1:RETURN 4570 ELSE RETURN
50 REM -------------- öÖÉÖóî öéâôò öÄâòó ---------
60 FOR I=0 TO 234:READ MEMO$(I),OP(I),LENG(I):LIS(OP(I))=I:NEXT I:FOR I=0 TO 15:READ PA(I),BAN$(I):NEXT:FOR I=0 TO 5:READ TF$(I):NEXT:GOTO 4480
70 DATA "???",0,1,ACI,&HCE,2,ADC A,&H8F,1,ADC B,&H88,1,ADC C,&H89,1,ADC D,&H8A,1,ADC E,&H8B,1,ADC H,&H8C,1,ADC L,&H8D,1,ADC M,8E,1,ADD A,&H87,1,ADD B,&H80,1,ADD C,&H81,1,ADD D,&H82,1,ADD E,&H82,1,ADD H,&H84,1,ADD L,&H85,1,ADD M,&H86,1
80 DATA ADI,&HC6,2,ANA A,&HA7,1,ANA B,&HA0,1,ANA C,&HA1,1,ANA D,&HA2,1,ANA E,&HA3,1,ANA H,&HA4,1,ANA L,&HA5,1,ANA M,&HA6,1,ANI,&HE6,2,CALL,&HCD,3,CC,&HDC,3,CM,&HFC,3,CMA,&H2F,1,CMC,&H3F,1,CMP A,&HBF,1,CMP B,&HB8,1
90 DATA CMP C,&HB9,1,CMP D,&HBA,1,CMP E,&HBB,1,CMP H,&HBC,1,CMP L,&HBD,1,CMP M,&HBE,1,CNC,&HD4,3,CNZ,&HC4,3,CP,&HF4,3,CPE,&HEC,3,CPI,&HFE,2,CPO,&HE4,3,CZ,&HCC,3,DAA,&H27,1,DAD B,&H09,1,DAD D,&H19,1,DAD H,&H29,1,DAD SP,&H39,1
100 DATA DCR A,&H3D,1,DCR B,&H05,1,DCR C,&H0D,1,DCR D,&H15,1,DCR E,&H1D,1,DCR H,&H25,1,DCR L,&H2D,1,DCR M,&H35,1,DCX B,&H0B,1,DCX D,&H1B,1,DCX H,&H2B,1,DCX SP,&H3B,1,HLT,&H76,1,IN,&HDB,2,INR A,&H3C,1,INR B,&H04,1,INR C,&H0C,1,INR D,&H14,1,INR E,&H1C,1
110 DATA INR H,&H24,1,INR L,&H2C,1,INR M,&H34,1,INX B,&H03,1,INX D,&H13,1,INX H,&H23,1,INX SP,&H33,1,JC,&HDA,3,JM,&HFA,3,JMP,&HC3,3,JNC,&HD2,3,JNZ,&HC2,3,JP,&HF2,3,JPE,&HEA,3,JPO,&HE2,3,JZ,&HCA,3,LDA,&H3A,3,LDAX B,&H0A,1,LDAX D,&H1A,1
120 DATA LHLD,&H2A,3,LXI B,&H01,3,LXI D,&H11,3,LXI H,&H21,3,LXI SP,&H31,3
130 DATA "MOV A,A",&H7F,1,"MOV A,B",&h78,1,"MOV A,C",&h79,1,"MOV A,D",&h7A,1,"MOV A,E",&H7B,1,"MOV A,H",&H7C,1,"MOV A,L",&h7D,1,"MOV A,M",&H7E,1,"MOV B,A",&H47,1,"MOV B,B",&H40,1,"MOV B,C",&H41,1,"MOV B,D",&H42,1,"MOV B,E",&H43,1
140 DATA "MOV B,H",&H44,1,"MOV B,L",&H45,1,"MOV B,M",&H46,1,"MOV C,A",&H4F,1,"MOV C,B",&H48,1,"MOV C,C",&H49,1,"MOV C,D",&H4A,1,"MOV C,E",&H4B,1,"MOV C,H",&H4C,1,"MOV C,L",&H4D,1,"MOV C,M",&H4E,1,"MOV D,A",&H57,1,"MOV D,B",&H50,1,"MOV D,C",&H51,1
150 DATA "MOV D,D",&H52,1,"MOV D,E",&H53,1,"MOV D,H",&H54,1,"MOV D,L",&H55,1,"MOV D,M",&H56,1,"MOV E,A",&H5F,1,"MOV E,B",&H58,1,"MOV E,C",&H59,1,"MOV E,D",&H5A,1,"MOV E,E",&H5B,1,"MOV E,H",&H5C,1,"MOV E,L",&H5D,1,"MOV E,M",&H5E,1,"MOV H,A",&H67,1
160 DATA "MOV H,B",&H60,1,"MOV H,C",&H61,1,"MOV H,D",&H62,1,"MOV H,E",&H63,1,"MOV H,H",&H64,1,"MOV H,L",&H65,1,"MOV H,M",&H66,1,"MOV L,A",&H6F,1,"MOV L,B",&H68,1,"MOV L,C",&H69,1,"MOV L,D",&H6A,1,"MOV L,E",&H6B,1,"MOV L,H",&H6C,1,"MOV L,L",&H6D,1
170 DATA "MOV L,M",&H6E,1,"MOV M,A",&H77,1,"MOV M,B",&H70,1,"MOV M,C",&H71,1,"MOV M,D",&H72,1,"MOV M,E",&H73,1,"MOV M,H",&H74,1,"MOV M,L",&H75,1,MVI A,&H3E,2,MVI B,&H06,2,MVI C,&H0E,2,MVI D,&H16,2,MVI E,&H1E,2,MVI H,&H26,2,MVI L,&H2E,2,MVI M,&H36,2
180 DATA NOP,&H00,1,ORA A,&HB7,1,ORA B,&HB0,1,ORA C,&HB1,1,ORA D,&HB2,1,ORA E,&HB3,1,ORA H,&HB4,1,ORA L,&HB4,1,ORA M,&HB6,1,ORI,&HF6,2,OUT,&HD3,2,PCHL,&HE9,1,POP B,&HC1,1,POP D,&HD1,1,POP H,&HE1,1,POP PSW,&HF1,1,PUSH B,&HC5,1,PUSH D,&HD5,1,PUSH H,&HE5,1
190 DATA PUSH PSW,&HF5,1,RAL,&H17,1,RAR,&H1F,1,RC,&HD8,1,RET,&HC9,1,RLC,&H07,1,RM,&HF8,1,RNC,&HD0,1,RNZ,&HC0,1,RP,&HF0,1,RPE,&HE8,1,RPO,&HE0,1,RRC,&H0F,1,RZ,&HC8,1,SBB A,&H9F,1,SBB B,&H98,1,SBB C,&H99,1,SBB D,&H9A,1,SBB E,&H9B,1,SBB H,&H9C,1,SBB L,&H9D,1
200 DATA SBB M,&H9E,1,SBI,&HDE,2,SHLD,&H22,3,SPHL,&HF9,1,STA,&H32,3,STAX B,&H02,1,STAX D,&H12,1,STC,&H37,1,SUB A,&H97,1,SUB B,&H90,1,SUB C,&H91,1,SUB D,&H92,1,SUB E,&H93,1,SUB H,&H94,1,SUB L,&H95,1,SUB M,&H96,1
210 DATA SUI,&HD6,2,XCHG,&HEB,1,XRA A,&HAF,1,XRA B,&HA8,1,XRA C,&HA9,1,XRA D,&HAA,1,XRA E,&HAB,1,XRA H,&HAC,1,XRA L,&HAD,1,XRA M,&HAE,1,XRI,&HEE,2,XTHL,&HE3,1
220 REM ------------ DATA PA , BAN$ ------
230 DATA 0,0000,1,0001,1,0010,0,0011,1,0100,1,0101,0,0110,1,0111,1,1000,0,1001,0,1010,1,1011,0,1100,1,1101,1,1110,0,1111
240 REM ------------ DATA TF ------
250 DATA LOAD,SAVE,PRT.,INS.,ESC.,QUIT
260 REM --------- OP CODE ÑëîÉÑ ----------
270 LOCATE LY,27:PRINT HEX$(LC+8192);": ";SPC(8):LOCATE LY,33:H$=HEX$(MEMORY(LC)):PRINT STRING$(2-LEN(H$),"0");H$:LOCATE LY,36:IF LENG(MAKOM)=1 THEN GOTO 290 ELSE H$=HEX$(MEMORY(LC+1)):PRINT STRING$(2-LEN(H$),"0");H$
280 IF LENG(MAKOM)=3 THEN LOCATE LY,39:H$=HEX$(MEMORY(LC+2)):PRINT STRING$(2-LEN(H$),"0");H$
290 GOSUB 40:RETURN
300 IF LY=23 THEN LY=4:BEEP:RETURN ELSE LY=LY+1:RETURN
310 REM --------- çâåÇÉÑ âÖêâô -------------
320 IF ZM<0 THEN S=1:IF ZM<-255 THEN ZM=ZM+255 ELSE S=0
330 IF ZM=0 THEN Z=1 ELSE Z=0
340 RETURN
350 REM ------------
360 IF ZM>255 THEN CY=1:ZM=ZM-256 ELSE CY=0
370 RETURN
380 REM ------------
390 ZM=PA(INT(ZM/16))+PA(ZM-INT(ZM/16)*16):IF ZM=0 OR ZM=2 THEN P=1 ELSE P=111
400 RETURN
410 REM --------- öâêãÖö öñâò çÖâëÅ çâòÇÖÄÑ öëîÉÑ ----------
420 LOCATE 6,63:I=AC:GOSUB 450:LOCATE 8,63:I=C:GOSUB 450:I=B:LOCATE 8,50:GOSUB 450:I=E:LOCATE 10,63:GOSUB 450:I=D:LOCATE 10,50:GOSUB 450:I=H:LOCATE 12,50:GOSUB 450:I=L:LOCATE 12,63:GOSUB 450:LOCATE 14,53:PRINT HEX$(SP+8192)
430 GOTO 4570
440 REM ----------- òÇÖÄå åÄéôé ~0~ öîëÖÑ --------
450 H$=RIGHT$(HEX$(I),2):H$=STRING$(2-LEN(H$),"0")+H$:PRINT H$:RETURN
460 REM ----------------  GO    öâêãÖöÑ öñòÑ  öåâáö ---------------
470 ON ERROR GOTO 480:AC=0:SP=6398:B=0:C=0:D=0:E=0:H=0:L=0:Z=0:CY=0:S=0:GOSUB 300:GOTO 510
480 RESUME 490
490 BEEP:BEEP:LOCATE LY,2:PRINT "Error in adrress $";HEX$(LC+8192):PRINT SPC(19):GOSUB 300:X=1:GOTO 410
500 LC=LC+LENG(LIS(OP))
510 OP=MEMORY(LC):T$=INKEY$:IF T$="B" THEN 580
520 ON LIS(OP)+1 GOTO 580,600,620,640,660,680,700,720,740,760,780,800,820,840,860,880,900,920,940,960,980,1000,1020,1040,1060,1080,1100,1120,1140,1160,1180,1200,1220,1240,1260,1280,1300,1320,1340,1360,1380,1400,1420,1440,1460,1480,1500,1520,1080
530 ON LIS(OP)-50 GOTO 1550,1570,1630,1650,1670,1690,1710,1730,1750,1770,1790,1810,1830,1850,1870,1890,1910,1930,1950,1970,1990,2010,2030,2050,2070,2090,2110,2130,2150,2170,2190,2210,2230,2250,2270,2290,2310,2330,2350,2370,2390,2410,2430,2450,2470
540 ON LIS(OP)-97 GOTO 2510,2520,2530,2540,2550,2560,2580,2590,2600,2610,2620,2630,2640,2650,2670,2680,2690,2700,2710,2720,2730,2740,2760,2770,2780,2790,2800,2810,2820,2830,2850,2860,2870,2880,2890,2900,2910,2920,2940,2950,2960,2970,2980,2990,3000
550 ON LIS(OP)-143 GOTO 3030,3040,3050,3060,3070,3080,3090,3100,3120,3130,3140,3150,3160,3170,3180,3200,3210,3220,3230,3240,3250,3260,3270,500,3290,3300,3310,3480,3330,3340,3350,3360,3370,3390,3420,3440,3460,3480,3500,3520,3540,3560,3580,3600,3640
560 ON LIS(OP)-188 GOTO 3660,3680,3700,3740,3760,3780,3800,3820,3840,3860,3880,3900,3910,3920,3930,3940,3950,3960,3970,3990,4010,4030,4050,4070,4090,4110,4130,4150,4160,4170,4180,4190,4200,4210,4220,4240,4260,4280,4290,4300,4310,4320,4330,4340,4350
570 REM ---------- ??? -----------
580 BEEP:BEEP:LOCATE LY,2:PRINT "Break in adrress $";HEX$(LC+8192);:PRINT SPC(19):GOSUB 300:X=1:GOTO 410
590 REM -------- ACI -----------
600 AC=AC+MEMORY(LC+1)+CY:ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
610 REM --------------- ADC A ---------
620 AC=AC+AC+CY:ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
630 REM --------------- ADC B ---------
640 AC=AC+B+CY:ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
650 REM --------------- ADC C ---------
660 AC=AC+C+CY:ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
670 REM --------------- ADC D ---------
680 AC=AC+D+CY:ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
690 REM --------------- ADC E ---------
700 AC=AC+E+CY:ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
710 REM --------------- ADC H ---------
720 AC=AC+H+CY:ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
730 REM --------------- ADC L ---------
740 AC=AC+L+CY:ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
750 REM --------------- ADC M ---------
760 AC=AC+MEMORY(H*256+L-8192)+CY:ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
770 REM --------------- ADD A ---------
780 AC=AC+AC:ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
790 REM --------------- ADD B ---------
800 AC=AC+B:ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
810 REM --------------- ADD C ---------
820 AC=AC+C:ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
830 REM --------------- ADD D ---------
840 AC=AC+D:ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
850 REM --------------- ADD E ---------
860 AC=AC+E:ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
870 REM --------------- ADD H ---------
880 AC=AC+H:ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
890 REM --------------- ADD L ---------
900 AC=AC+L:ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
910 REM --------------- ADD M ---------
920 AC=AC+MEMORY(H*256+L-8192):ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
930 REM --------------- ADI ---------
940 AC=AC+MEMORY(LC+1):ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
950 REM --------------- ANA A ---------
960 AC=AC AND AC:ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
970 REM --------------- ANA B ---------
980 AC=AC AND B:ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
990 REM --------------- ANA C ---------
1000 AC=AC AND C:ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
1010 REM --------------- ANA D ---------
1020 AC=AC AND D:ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
1030 REM --------------- ANA E ---------
1040 AC=AC AND E:ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
1050 REM --------------- ANA H ---------
1060 AC=AC AND H:ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
1070 REM --------------- ANA L ---------
1080 AC=AC AND L:ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
1090 REM --------------- ANA M ---------
1100 AC=AC AND MEMORY(H*256+L-8192):ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
1110 REM --------------- ANI ---------
1120 AC=AC AND MEMORY(LC+1):ZM=AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
1130 REM --------------- CALL -----------------
1140 SP=SP-1:MEMORY(SP)=INT((LC+8195)/256):SP=SP-1:MEMORY(SP)=LC+8195-INT((LC+8195)/256)*256:LC=256*MEMORY(LC+2)+MEMORY(LC+1)-8192:GOTO 510
1150 REM --------------- CC -----------------
1160 IF CY=1 THEN 1140 ELSE 500
1170 REM --------------- CM -----------------
1180 IF S=1 THEN 1140 ELSE 500
1190 REM --------------- CMA -----------------
1200 AC=255-AC:GOTO 500
1210 REM ---------------- CMC ---------------
1220 CY=1-CY:GOTO 500
1230 REM ---------------- CMP A -------------
1240 ZM=AC-AC:GOSUB 320:GOSUB 360:GOTO 500
1250 REM ---------------- CMP B -------------
1260 ZM=AC-B:GOSUB 320:GOSUB 360:GOTO 500
1270 REM ---------------- CMP C -------------
1280 ZM=AC-C:GOSUB 320:GOSUB 360:GOTO 500
1290 REM ---------------- CMP D -------------
1300 ZM=AC-D:GOSUB 320:GOSUB 360:GOTO 500
1310 REM ---------------- CMP E -------------
1320 ZM=AC-E:GOSUB 320:GOSUB 360:GOTO 500
1330 REM ---------------- CMP H -------------
1340 ZM=AC-H:GOSUB 320:GOSUB 360:GOTO 500
1350 REM ---------------- CMP L -------------
1360 ZM=AC-L:GOSUB 320:GOSUB 360:GOTO 500
1370 REM ---------------- CMP M -------------
1380 ZM=AC-MEMORY(H*256+L-8192):GOSUB 320:GOSUB 360:GOTO 500
1390 REM ---------------- CNC -------------
1400 IF CY=0 THEN 1140 ELSE 500
1410 REM ---------------- CNZ --------------
1420 IF Z=0 THEN 1140 ELSE 500
1430 REM ---------------- CP ---------------
1440 IF S=0 THEN 1140 ELSE 500
1450 REM ---------------- CPE ---------------
1460 GOSUB 390:IF P=1 THEN 1140 ELSE 500
1470 REM ---------------- CPI ---------------
1480 ZM=AC-MEMORY(LC+1):GOSUB 320:GOSUB 360:GOTO 500
1490 REM ----------------- CPO -----------------
1500 GOSUB 390:IF P=0 THEN 1140 ELSE 500
1510 REM ----------------- CZ ------------------
1520 IF Z=1 THEN 1140 ELSE 500
1530 REM ***************** DAA ******************
1540 REM ----------------- DAD B ----------------
1550 ZM=H*256+L+B*256+C:H=INT(ZM/256):L=ZM-H*256:IF H>255 THEN CY=1:H=H-256:GOTO 500 ELSE GOTO 500
1560 REM ----------------- DAD D ----------------
1570 ZM=H*256+L+D*256+E:H=INT(ZM/256):L=ZM-H*256:IF H>255 THEN CY=1:H=H-256:GOTO 500 ELSE GOTO 500
1580 REM ----------------- DAD H ----------------
1590 ZM=2*(H*256+L):H=INT(ZM/256):L=ZM-H*256:IF H>255 THEN CY=1:H=H-256:GOTO 500 ELSE GOTO 500
1600 REM ----------------- DAD SP ---------------
1610 ZM=H*256+L+SP+8192:H=INT(ZM/256):L=ZM-H*256:IF H>255 THEN CY=1:H=H-256:GOTO 500 ELSE GOTO 500
1620 REM ----------------- DCR A ----------------
1630 AC=AC-1:ZM=AC:GOSUB 320:AC=ZM:GOTO 500
1640 REM ----------------- DCR B ----------------
1650 B=B-1:ZM=B:GOSUB 320:B=ZM:GOTO 500
1660 REM ----------------- DCR C ----------------
1670 C=C-1:ZM=C:GOSUB 320:C=ZM:GOTO 500
1680 REM ----------------- DCR D ----------------
1690 D=D-1:ZM=D:GOSUB 320:D=ZM:GOTO 500
1700 REM ----------------- DCR E ----------------
1710 E=E-1:ZM=E:GOSUB 320:E=ZM:GOTO 500
1720 REM ----------------- DCR H ----------------
1730 H=H-1:ZM=H:GOSUB 320:H=ZM:GOTO 500
1740 REM ----------------- DCR L ----------------
1750 L=L-1:ZM=L:GOSUB 320:L=ZM:GOTO 500
1760 REM ----------------- DCR M ----------------
1770 I=H*256+L-8192:MEMORY(I)=MEMORY(I)-1:ZM=MEMORY(I):GOSUB 320:MEMORY(I)=ZM:GOTO 500
1780 REM ----------------- DCX B ----------------
1790 ZM=B*256+C-1:B=INT(ZM/256):C=ZM-B*256:GOTO 500
1800 REM ----------------- DCX D ----------------
1810 ZM=D*256+E-1:D=INT(ZM/256):E=ZM-D*256:GOTO 500
1820 REM ----------------- DCX H ----------------
1830 ZM=H*256+L-1:H=INT(ZM/256):L=ZM-H*256:GOTO 500
1840 REM ----------------- DCX SP ---------------
1850 SP=SP-1:GOTO 500
1860 REM ------------------ HLT ----------------
1870 GOTO 580
1880 REM ---------------- IN --------------------
1890 BEEP:LOCATE LY,2:PRINT "Please enter number by port 00. <ENTER>.":GOSUB 300:GOSUB 4390:AC=0:FOR I=0 TO 7:AC=AC+2^(I)*VAL(MID$(KL$,8-I,1)):NEXT:GOTO 500
1900 REM ---------------- INR A -----------------
1910 AC=AC+1:ZM=AC:GOSUB 320:AC=ZM:GOTO 500
1920 REM ---------------- INR B -----------------
1930 B=B+1:ZM=B:GOSUB 320:B=ZM:GOTO 500
1940 REM ---------------- INR C -----------------
1950 C=C+1:ZM=C:GOSUB 320:C=ZM:GOTO 500
1960 REM ---------------- INR D -----------------
1970 D=D+1:ZM=D:GOSUB 320:D=ZM:GOTO 500
1980 REM ---------------- INR E -----------------
1990 E=E+1:ZM=E:GOSUB 320:E=ZM:GOTO 500
2000 REM ---------------- INR H -----------------
2010 H=H+1:ZM=H:GOSUB 320:H=ZM:GOTO 500
2020 REM ---------------- INR L -----------------
2030 L=L+1:ZM=L:GOSUB 320:L=ZM:GOTO 500
2040 REM ---------------- INR M -----------------
2050 I=H*256+L-8192:MEMORY(I)=MEMORY(I)+1:ZM=MEMORY(I):GOSUB 320:MEMORY(I)=ZM:GOTO 500
2060 REM ----------------- INX B ----------------
2070 ZM=B*256+C+1:B=INT(ZM/256):C=ZM-B*256:GOTO 500
2080 REM ----------------- INX D ----------------
2090 ZM=D*256+E+1:D=INT(ZM/256):E=ZM-D*256:GOTO 500
2100 REM ----------------- INX H ----------------
2110 ZM=H*256+L+1:H=INT(ZM/256):L=ZM-H*256:GOTO 500
2120 REM ----------------- INX SP ----------------
2130 SP=SP+1:GOTO 500
2140 REM ---------------- JC ----------------
2150 IF CY=1 THEN 2190
2160 REM ---------------- JM ---------------
2170 IF S=1 THEN 2190 ELSE 500
2180 REM ---------------- JMP ---------------
2190 LC=MEMORY(LC+2)*256+MEMORY(LC+1)-8192:GOTO 510
2200 REM ---------------- JNC ---------------
2210 IF CY=0 THEN 2190 ELSE 500
2220 REM ---------------- JNZ ---------------
2230 IF Z=0 THEN 2190 ELSE 500
2240 REM ---------------- JP ----------------
2250 IF S=0 THEN 2190 ELSE 500
2260 REM ---------------- JPE ---------------
2270 GOSUB 390:IF P=1 THEN 2190 ELSE 500
2280 REM ---------------- JPO ---------------
2290 GOSUB 390:IF P=0 THEN 2190 ELSE 500
2300 REM ---------------- JZ ----------------
2310 IF Z=1 THEN 2190 ELSE 500
2320 REM ---------------- LDA ---------------
2330 AC=MEMORY(MEMORY(LC+2)*256+MEMORY(LC+1)-8192):GOTO 500
2340 REM ---------------- LDAX B ------------
2350 AC=MEMORY(B*256+C-8192):GOTO 500
2360 REM ---------------- LDAX D ------------
2370 AC=MEMORY(D*256+E-8192):GOTO 500
2380 REM ---------------- LHLD --------------
2390 I=MEMORY(LC+2)*256+MEMORY(LC+1)-8192:L=MEMORY(I):H=MEMORY(I+1):GOTO 500
2400 REM ---------------- LXI B --------------
2410 B=MEMORY(LC+2):C=MEMORY(LC+1):GOTO 500
2420 REM ---------------- LXI D --------------
2430 D=MEMORY(LC+2):E=MEMORY(LC+1):GOTO 500
2440 REM ---------------- LXI H --------------
2450 H=MEMORY(LC+2):L=MEMORY(LC+1):GOTO 500
2460 REM ---------------- LXI SP -------------
2470 SP=MEMORY(LC+2)*256+MEMORY(LC+1)-8192:GOTO 500
2480 REM ---------------- MOV A,* -------------
2490 AC=AC:GOTO 500
2500 AC=B:GOTO 500
2510 AC=C:GOTO 500
2520 AC=D:GOTO 500
2530 AC=E:GOTO 500
2540 AC=H:GOTO 500
2550 AC=L:GOTO 500
2560 AC=MEMORY(H*256+L-8192):GOTO 500
2570 REM ---------------- MOV B,* -------------
2580 B=AC:GOTO 500
2590 B=B:GOTO 500
2600 B=C:GOTO 500
2610 B=D:GOTO 500
2620 B=E:GOTO 500
2630 B=H:GOTO 500
2640 B=L:GOTO 500
2650 B=MEMORY(H*256+L-8192):GOTO 500
2660 REM ---------------- MOV C,* -------------
2670 C=AC:GOTO 500
2680 C=B:GOTO 500
2690 C=C:GOTO 500
2700 C=D:GOTO 500
2710 C=E:GOTO 500
2720 C=H:GOTO 500
2730 C=L:GOTO 500
2740 C=MEMORY(H*256+L-8192):GOTO 500
2750 REM ---------------- MOV D,* -------------
2760 D=AC:GOTO 500
2770 D=B:GOTO 500
2780 D=C:GOTO 500
2790 D=D:GOTO 500
2800 D=E:GOTO 500
2810 D=H:GOTO 500
2820 D=L:GOTO 500
2830 D=MEMORY(H*256+L-8192):GOTO 500
2840 REM ---------------- MOV E,* -------------
2850 E=AC:GOTO 500
2860 E=B:GOTO 500
2870 E=C:GOTO 500
2880 E=D:GOTO 500
2890 E=E:GOTO 500
2900 E=H:GOTO 500
2910 E=L:GOTO 500
2920 E=MEMORY(H*256+L-8192):GOTO 500
2930 REM ---------------- MOV H,* -------------
2940 H=AC:GOTO 500
2950 H=B:GOTO 500
2960 H=C:GOTO 500
2970 H=D:GOTO 500
2980 H=E:GOTO 500
2990 H=H:GOTO 500
3000 H=L:GOTO 500
3010 H=MEMORY(H*256+L-8192):GOTO 500
3020 REM ---------------- MOV L,* -------------
3030 L=AC:GOTO 500
3040 L=B:GOTO 500
3050 L=C:GOTO 500
3060 L=D:GOTO 500
3070 L=E:GOTO 500
3080 L=H:GOTO 500
3090 L=L:GOTO 500
3100 L=MEMORY(H*256+L-8192):GOTO 500
3110 REM ---------------- MOV M,* -------------
3120 MEMORY(H*256+L-8192)=AC:GOTO 500
3130 MEMORY(H*256+L-8192)=B:GOTO 500
3140 MEMORY(H*256+L-8192)=C:GOTO 500
3150 MEMORY(H*256+L-8912)=D:GOTO 500
3160 MEMORY(H*256+L-8192)=E:GOTO 500
3170 MEMORY(H*256+L-8192)=H:GOTO 500
3180 MEMORY(H*256+L-8192)=L:GOTO 500
3190 REM ---------------- MVI * -----------------
3200 AC=MEMORY(LC+1):GOTO 500
3210 B=MEMORY(LC+1):GOTO 500
3220 C=MEMORY(LC+1):GOTO 500
3230 D=MEMORY(LC+1):GOTO 500
3240 E=MEMORY(LC+1):GOTO 500
3250 H=MEMORY(LC+1):GOTO 500
3260 L=MEMORY(LC+1):GOTO 500
3270 MEMORY(H*256+L-8192)=MEMORY(LC+1):GOTO 500
3280 REM ------------- ORA * -----------
3290 ZM=AC OR AC:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
3300 ZM=AC OR B:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
3310 ZM=AC OR C:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
3320 ZM=AC OR D:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
3330 ZM=AC OR E:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
3340 ZM=AC OR H:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
3350 ZM=AC OR L:GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
3360 ZM=AC OR MEMORY(H*256+L-8192):GOSUB 320:GOSUB 360:AC=ZM:GOTO 500
3370 ZM=AC OR MEMORY(LC+1):GOSUB 320:GOSUB 360:GOTO 500
3380 REM ----------- OUT ------------------
3390 ZM$=RIGHT$(HEX$(AC),2):ZM$=STRING$(2-LEN(ZM$),"0")+ZM$:ZM$=BAN$(VAL("&H"+LEFT$(ZM$,1)))+BAN$(VAL("&H"+RIGHT$(ZM$,1))):LOCATE 19,54:FOR I=0 TO 7:IF MID$(ZM$,I+1,1)="1" THEN PRINT "±"; ELSE PRINT "˛";
3400 PRINT "≥";:NEXT:GOTO 500
3410 REM ------------- PCHL ----------------
3420 LC=H*256+L-8192:GOTO 510
3430 REM ------------- POP B ---------------
3440 C=MEMORY(SP):SP=SP+1:B=MEMORY(SP):SP=SP+1:GOTO 500
3450 REM ------------- POP D ---------------
3460 E=MEMORY(SP):SP=SP+1:D=MEMORY(SP):SP=SP+1:GOTO 500
3470 REM ------------- POP H ---------------
3480 L=MEMORY(SP):SP=SP+1:H=MEMORY(SP):SP=SP+1:GOTO 500
3490 REM ------------- POP PSW -------------
3500 SP=MEMORY(SP)+MEMORY(SP+1)*256-8192:GOTO 500
3510 REM ------------- PUSH B --------------
3520 SP=SP-1:MEMORY(SP)=B:SP=SP-1:MEMORY(SP)=C:GOTO 500
3530 REM ------------- PUSH D --------------
3540 SP=SP-1:MEMORY(SP)=D:SP=SP-1:MEMORY(SP)=E:GOTO 500
3550 REM ------------- PUSH H --------------
3560 SP=SP-1:MEMORY(SP)=H:SP=SP-1:MEMORY(SP)=L:GOTO 500
3570 REM ------------- PUSH PSW ------------
3580 MEMORY(SP-1)=INT((SP+8192)/256):MEMORY(SP-2)=SP+8192-INT((SP+8192)/256)*256:SP=SP-2:GOTO 500
3590 REM ------------ RAL -----------------
3600 I=CY:IF AC>127 THEN CY=1 ELSE CY=0
3610 AC=AC+AC+I:IF AC>255 THEN AC=AC-256
3620 GOTO 500
3630 REM ------------ RAR -----------------
3640 I=CY:CY=AC AND 1:AC=INT(AC-AC/2):AC=AC+2^7*I:GOTO 500
3650 REM ------------ RC ------------------
3660 IF CY=1 THEN 3680 ELSE 500
3670 REM ------------ RET -----------------
3680 LC=MEMORY(SP)+MEMORY(SP+1)*256-8192:SP=SP+2:GOTO 510
3690 REM ------------ RLC -----------------
3700 IF AC>127 THEN CY=1 ELSE CY=0
3710 AC=AC+AC+CY:IF AC>255 THEN AC=AC-256
3720 GOTO 500
3730 REM ------------ RM ------------------
3740 IF S=1 THEN 3680 ELSE 500
3750 REM ------------ RNC -----------------
3760 IF CY=0 THEN 3680 ELSE 500
3770 REM ------------ RNZ -----------------
3780 IF Z=0 THEN 3680 ELSE 500
3790 REM ------------ RP ------------------
3800 IF S=0 THEN 3680 ELSE 500
3810 REM ------------ RPE -----------------
3820 GOSUB 390:IF P=1 THEN 3680 ELSE 500
3830 REM ------------ RPO -----------------
3840 GOSUB 390:IF P=0 THEN 3680 ELSE 500
3850 REM ------------ RRC -----------------
3860 CY=AC AND 1:AC=INT(AC-AC/2)+CY*2^7:GOTO 500
3870 REM ------------ RZ ------------------
3880 IF Z=1 THEN 3680 ELSE 500
3890 REM ------------ SBB * ---------------
3900 AC=0-CY:GOSUB 320:GOSUB 360:GOTO 500
3910 AC=AC-B-CY:GOSUB 320:GOSUB 360:GOTO 500
3920 AC=AC-C-CY:GOSUB 320:GOSUB 360:GOTO 500
3930 AC=AC-D-CY:GOSUB 320:GOSUB 360:GOTO 500
3940 AC=AC-E-CY:GOSUB 320:GOSUB 360:GOTO 500
3950 AC=AC-H-CY:GOSUB 320:GOSUB 360:GOTO 500
3960 AC=AC-L-CY:GOSUB 320:GOSUB 360:GOTO 500
3970 AC=AC-MEMORY(H*256+L-8192)-CY:GOSUB 320:GOSUB 360:GOTO 500
3980 AC=AC-MEMORY(LC+1)-CY:GOSUB 320:GOSUB 360:GOTO 500
3990 AC=AC-MEMORY(LC+1)-CY:GOSUB 320:GOSUB 360:GOTO 500
4000 REM -------------- SHLD -------------
4010 I=MEMORY(LC+1)+MEMORY(LC+2)*256-8192:MEMORY(I)=L:MEMORY(I+1)=H:GOTO 500
4020 REM -------------- SPHL -------------
4030 SP=H*256+L:GOTO 500
4040 REM -------------- STA --------------
4050 MEMORY(MEMORY(LC+1)+MEMORY(LC+2)*256-8192)=AC:GOTO 500
4060 REM -------------- STAX B -----------
4070 MEMORY(B*256+C-8192)=AC:GOTO 500
4080 REM -------------- STAX D -----------
4090 MEMORY(D*256+E-8192)=AC:GOTO 500
4100 REM -------------- STC --------------
4110 CY=1:GOTO 500
4120 REM -------------- SUB * ------------
4130 AC=0:GOSUB 320:GOSUB 360:GOTO 500
4140 REM -------------- SUB * ------------
4150 AC=AC-B:GOSUB 320:GOSUB 360:GOTO 500
4160 AC=AC-C:GOSUB 320:GOSUB 360:GOTO 500
4170 AC=AC-D:GOSUB 320:GOSUB 360:GOTO 500
4180 AC=AC-E:GOSUB 320:GOSUB 360:GOTO 500
4190 AC=AC-H:GOSUB 320:GOSUB 360:GOTO 500
4200 AC=AC-L:GOSUB 320:GOSUB 360:GOTO 500
4210 AC=AC-MEMORY(H*256+L-8192):GOSUB 320:GOSUB 360:GOTO 500
4220 AC=AC-MEMORY(LC+1):GOSUB 320:GOSUB 360:GOTO 500
4230 REM -------------- XCHG ---------------
4240 I=H:H=D:D=I:SWAP L,E:GOTO 500
4250 REM -------------- XRA * --------------
4260 AC=0:GOSUB 320:GOSUB 360:GOTO 500
4270 REM -------------- XRA * --------------
4280 AC=AC XOR B:GOSUB 320:GOSUB 360:GOTO 500
4290 AC=AC XOR C:GOSUB 320:GOSUB 360:GOTO 500
4300 AC=AC XOR D:GOSUB 320:GOSUB 360:GOTO 500
4310 AC=AC XOR E:GOSUB 320:GOSUB 360:GOTO 500
4320 AC=AC XOR H:GOSUB 320:GOSUB 360:GOTO 500
4330 AC=AC XOR L:GOSUB 320:GOSUB 360:GOTO 500
4340 AC=AC XOR MEMORY(H*256+L-8192):GOSUB 320:GOSUB 360:GOTO 500
4350 AC=AC XOR MEMORY(LC+1):GOSUB 320:GOSUB 360:GOTO 500
4360 REM -------------- XTHL  --------------
4370 SWAP L,MEMORY(SP):SWAP H,MEMORY(SP+1):GOTO 500
4380 REM --------- çâôóé â~í àåó --------------
4390 KEY(12)ON:KEY(13)ON:LX=0
4400 LOCATE 23,LX*2+54:COLOR 0,7:PRINT "Ô":COLOR 7,0
4410 ON KEY (13) GOSUB 4440
4420 ON KEY (12) GOSUB 4450
4430 T$=INKEY$:IF T$=" " THEN GOSUB 4470:GOTO 4430 ELSE IF T$=CHR$(13) THEN KEY(12)OFF:KEY(13)OFF:GOSUB 4460:RETURN ELSE IF T$="B" THEN GOSUB 4460:X=1:A$=SPACE$(23):GOTO 4570 ELSE 4430
4440 IF LX=7 THEN RETURN ELSE GOSUB 4460:LX=LX+1:RETURN 4400
4450 IF LX=0 THEN RETURN ELSE GOSUB 4460:LX=LX-1:RETURN 4400
4460 LOCATE 23,LX*2+54:PRINT " ":RETURN
4470 I=2*LX+54:IF SCREEN(21,I)=220 THEN MID$(KL$,LX+1,1)="1":LOCATE 21,I:PRINT "ﬂ":RETURN ELSE MID$(KL$,LX+1,1)="0":LOCATE 21,I:PRINT "‹":RETURN
4480 CLS
4490 LOCATE  1 , 1 :PRINT "…"STRING$(77,"Õ");"ª"
4500 LOCATE  2 , 1 :PRINT "∫"SPC(77)"∫"
4510 COLOR 0,7:LOCATE 2,13:PRINT "åÅÖâ åà & óòé àåÅÉåÖÇ â~í Åöãê \ 8085 ÉÅíéÑ åô ÑâéÉÑ":COLOR 7,0
4520 LOCATE  3 , 1 :PRINT "«ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ∂"
4530 FOR I=4 TO 23:PRINT "∫"SPC(41)"≥"SPC(35)"∫":NEXT
4540 PRINT "»"STRING$(41,"Õ")"œ"STRING$(35,"Õ")"º";:GOSUB 5770
4550 REM ----------  ÑÅâöã - èéëÑ öÜÜÑ ------------------
4560 X=1:LY=4
4570 COLOR 0,7:LOCATE LY,X+1:PRINT CHR$(SCREEN(LY,X+1)):COLOR 7,0:LOCATE LY,X+1
4580 GOSUB 4720:ON KEY (12) GOSUB 4670
4590 ON KEY (13) GOSUB 4680
4600 ON KEY (14) GOSUB 4690
4610 ON KEY (11) GOSUB 4700
4620 T$=INKEY$:IF NOT(T$="#" OR T$="$" OR T$=CHR$(13) OR T$=" " OR T$="," OR T$="-" OR T$=CHR$(27) OR (T$>="0" AND T$<="9") OR (T$>="A" AND T$<="Z")) THEN 4620
4630 IF T$=CHR$(13) THEN PRINT CHR$(SCREEN(LY,X+1)):GOSUB 4730:A$="":FOR I=2 TO 25:A$=A$+CHR$(SCREEN(LY,I)):NEXT I:GOTO 4740
4640 IF T$=CHR$(27) THEN GOSUB 4730:BEEP:GOTO 5120
4650 PRINT T$:IF X<24 THEN X=X+1
4660 GOTO 4570
4670 IF X=1 THEN RETURN ELSE GOSUB 4730:PRINT CHR$(SCREEN(LY,X+1)):X=X-1:RETURN 4570
4680 IF X=24 THEN RETURN ELSE PRINT CHR$(SCREEN(LY,X+1)):X=X+1:GOSUB 4730:RETURN 4570
4690 IF LY=23 THEN RETURN ELSE GOSUB 4730:PRINT CHR$(SCREEN(LY,X+1)):LY=LY+1:RETURN 4570
4700 IF LY=4 THEN RETURN ELSE GOSUB 4730:PRINT CHR$(SCREEN(LY,X+1)):LY=LY-1:RETURN 4570
4710 REM -----------
4720 KEY (11)ON:KEY (12)ON:KEY (13)ON:KEY (14)ON:RETURN
4730 KEY (11)OFF:KEY (12) OFF:KEY (13)OFF:KEY (14)OFF:RETURN
4740 REM --------------  áÖêíâî  ---------------
4750 T$=LEFT$(A$,1):IF T$="C" THEN 5100
4760 IF T$=" " THEN 4850
4770 LC=VAL("&h"+MID$(A$,2,4))-8192
4780 IF LC>8192 OR LC<0 THEN X=1:BEEP:GOTO 4570
4790 IF T$="A" THEN 4860
4800 IF T$="G" THEN 460
4810 IF T$="O" THEN 5040
4820 LCEND=VAL("&H"+MID$(A$,7,4))-8192
4830 IF T$="L" THEN 5010
4840 IF T$="I" THEN 5070
4850 X=1:GOSUB 300:GOTO 4570
4860 REM ------------ A$ çÖñéâñ   --------------
4870 FOR I=7 TO 20
4880 IF ((MID$(A$,I+1,1)=" " OR MID$(A$,I+1,1)=",") AND (MID$(A$,I+2,1)=" " OR MID$(A$,I+2,1)="#" OR MID$(A$,I+2,1)="$")) THEN PQ$=MID$(A$,7,I-6):NATOON$=MID$(A$,I+2,5):GOTO 4900
4890 NEXT I
4900 REM ---------- DATA Ñ öéâôòÅ ÑÉÖóîÑ áÖêíî ------
4910 IF PQ$=" " THEN X=1:GOSUB 300:GOTO 4570:REMÑÉÖóî èâÄô ÑòóéÅ
4920 A=1:MAKOM=0:FOR I=7 TO 0 STEP -1:MAKOM=MAKOM+2^I*A:IF MAKOM>234 THEN MAKOM=234
4930 IF MEMO$(MAKOM)=PQ$ THEN 4960 ELSE IF MEMO$(MAKOM)<PQ$ THEN A=1 ELSE A=-1
4940 NEXT I:X=7:BEEP:GOTO 4570:REM * ÑéâôòÅ öÄñéê Äå ÑÉÖóî
4950 REM --------- èÖòãâÜå ÑëêãÑ ----------
4960 MEMORY(LC)=OP(MAKOM)
4970 IF LENG(MAKOM)=2 THEN MEMORY(LC+1)=VAL("&H"+MID$(NATOON$,3,2)) ELSE IF LENG(MAKOM)=3 THEN MEMORY(LC+1)=VAL("&H"+RIGHT$(NATOON$,2)):MEMORY(LC+2)=VAL("&H"+MID$(NATOON$,2,2))
4980 REM --------- OP CODE ÑëîÉÑ ----------
4990 GOSUB 260:GOSUB 300:LOCATE LY,2:LC=LC+LENG(MAKOM):PRINT "A";HEX$(LC+8192);SPC(36):X=7:GOTO 4570
5000 REM ------------ MEMONIX Å öâêãÖöÑ åô LIST -----------
5010 GOSUB 300:MAKOM=LIS(MEMORY(LC)):LOCATE LY,2:I=0:M$="A"+HEX$(LC+8192)+" "+MEMO$(MAKOM)+" ":IF LENG(MAKOM)=1 THEN GOTO 5030 ELSE IF LENG(MAKOM)=2 THEN H$=HEX$(MEMORY(LC+1)):M$=M$+"#$"+STRING$(2-LEN(H$),"0")+H$:GOTO 5030
5020 H$=HEX$(MEMORY(LC+2)):M$=M$+"$"+STRING$(2-LEN(H$),"0")+H$:H$=HEX$(MEMORY(LC+1)):M$=M$+STRING$(2-LEN(H$),"0")+H$
5030 PRINT M$;SPC(15):GOSUB 260:LC=LC+LENG(MAKOM):IF LC>LCEND THEN GOSUB 300:LOCATE LY,2:PRINT SPC(40):X=1:GOTO 4570 ELSE GOTO 5010
5040 REM ----------  OP CODE  Å  ÑÅâöã --------------
5050 FOR I=0 TO 5:NA$=MID$(A$,7+I*3,2):IF NA$="  " THEN 5060 ELSE MEMORY(LC+I)=VAL("&h"+NA$):NEXT I
5060 GOSUB 300:IF I=0 THEN X=1:GOTO 4570 ELSE X=7:LOCATE LY,2:PRINT "O"+HEX$(LC+8192+I)+SPACE$(35):GOTO 4570
5070 REM --------------   OP CODE - Å LIST ---------
5080 GOSUB 300:GOSUB 40:LOCATE LY,2:PRINT "O";HEX$(LC+8192);:FOR I=0 TO 5:H$=HEX$(MEMORY(LC)):PRINT " ";STRING$(2-LEN(H$),"0");H$;:LC=LC+1:NEXT I:PRINT SPC(18)
5090 GOSUB 40:IF LC>LCEND THEN GOSUB 300:LOCATE LY,2:PRINT SPC(40):X=1:GOTO 4570 ELSE 5080
5100 REM ------------  CLS  äëéÑ âÖóâê -----------
5110 FOR LY=0 TO 9:LOCATE LY+14,2:PRINT SPC(41):LOCATE 13-LY,2:PRINT SPC(41):NEXT LY:GOTO 4560
5120 FOR I=0 TO 1:A$(I)="":FOR LX=2 TO 40:A$(I)=A$(I)+CHR$(SCREEN(I+22,LX)):NEXT LX,I:LX=0
5130 COLOR 7,0:LOCATE 22,1:PRINT "Ã";:PRINT STRING$(41,205);:PRINT "µ":LOCATE 24,8:PRINT "Õ";
5140 LOCATE 23,2:PRINT "    ";:FOR I=0 TO 5:PRINT TF$(I);"  ";:NEXT:GOSUB 5210
5150 KEY (13) ON:KEY (12) ON
5160 ON KEY(13) GOSUB 5190
5170 ON KEY(12) GOSUB 5200
5180 T$=INKEY$:IF T$=CHR$(13) THEN 5250 ELSE IF T$=CHR$(27) THEN 5270 ELSE 5180
5190 GOSUB 5230:IF LX=5 THEN RETURN 5150 ELSE GOSUB 5220:LX=LX+1:GOSUB 5210:RETURN 5150
5200 GOSUB 5230:IF LX=0 THEN RETURN 5150 ELSE GOSUB 5220:LX=LX-1:GOSUB 5210:RETURN 5150
5210 LOCATE 23,LX*6+6:COLOR 15,0:PRINT TF$(LX):COLOR 7,0:RETURN
5220 LOCATE 23,LX*6+6:PRINT TF$(LX):RETURN
5230 KEY(12)OFF:KEY(13)OFF:RETURN
5240 REM -----------------------------------
5250 ON LX+1 GOTO 5310,5370,5420,5420,5270,5290
5260 REM -------------- ESC -----------------
5270 LOCATE 22,1:PRINT "∫";A$(0);"  ≥":LOCATE 23,2:PRINT A$(1):GOTO 4570
5280 REM -------------- QUIT ----------------
5290 CLS:END
5300 REM -------------- LOAD ----------------
5310 LOCATE 22,8:PRINT "À":COLOR 15,0:LOCATE 23,3:PRINT "LOAD";:COLOR 7,0:PRINT " ∫ ";"ENTER FILE NAME : ";:COLOR 15,0:PRINT "________";:COLOR 7,0:PRINT "    ":LOCATE 24,8:PRINT " ";:GOSUB 5580:COLOR 7,0
5320 ON ERROR GOTO 5330:OPEN "I",#1,SH$+".DAT":GOTO 5340
5330 LOCATE 23,10:PRINT SPC(7);:COLOR 0,7:PRINT "DISK NOT READY";:COLOR 7,0:PRINT SPC(10):FOR I=1 TO 3000:NEXT I:RESUME 5130
5340 INPUT #1,T$:LC=VAL("&H"+T$)-8192
5350 INPUT #1,T$:IF EOF(1) THEN CLOSE:LOCATE 23,10:PRINT SPC(9);:COLOR 0,7:PRINT"PROGRAM LOADED";:COLOR 7,0:PRINT SPC(5):FOR I=1 TO 3000:NEXT I:GOTO 5130 ELSE MEMORY(LC)=VAL("&H"+T$):LC=LC+1:GOTO 5350
5360 REM -------------- SAVE ----------------
5370 LOCATE 22,8:PRINT "À":COLOR 15,0:LOCATE 23,3:PRINT "SAVE";:COLOR 7,0:PRINT " ∫ ";"ENTER FILE NAME : ";:COLOR 15,0:PRINT "________";:COLOR 7,0:PRINT "    ":LOCATE 24,8:PRINT " ";:GOSUB 5580:COLOR 7,0:LOCATE 23,21:PRINT "ADDRESS : ";
5380 COLOR 15,0:PRINT "____-____":GOSUB 5670
5390 ON ERROR GOTO 5330:OPEN "O",#1,SH$+".DAT":WRITE #1,MID$(ADD$,1,4):FOR I=VAL("&H"+MID$(ADD$,1,4))-8192 TO VAL("&H"+MID$(ADD$,6,4))-8189:WRITE #1,HEX$(MEMORY(I)):NEXT:CLOSE:LOCATE 23,10:PRINT SPC(9);:COLOR 0,7:PRINT "FILE SAVED";:COLOR 7,0
5400 PRINT SPC(13):FOR I=1 TO 3000:NEXT I:GOTO 5130
5410 REM -------------- PRT.    ----------
5420 LOCATE 22,8:PRINT "À":COLOR 15,0:LOCATE 23,3:PRINT "PRT.";:COLOR 7,0:PRINT " ∫ ";"ENTER FILE ADDRESS : ";:COLOR 15,0:PRINT "____-____   ":COLOR 7,0:LOCATE 24,8:PRINT " ";:GOSUB 5670:ON ERROR GOTO 5430:GOTO 5440
5430 LOCATE 23,10:PRINT SPC(5);:COLOR 0,7:PRINT "PRINTER NOT READY";:COLOR 7,0:PRINT SPC(5):FOR I=1 TO 3000:NEXT I:RESUME 5130
5440 LPRINT CHR$(27);CHR$(69);CHR$(14);"⁄ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø"
5450 LPRINT CHR$(27);CHR$(69);CHR$(14);"≥ PRT. ≥    ADREESS: ";ADD$;"   ≥"
5460 LPRINT CHR$(27);CHR$(69);CHR$(14);"√ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥"
5470 LPRINT CHR$(27);CHR$(69);CHR$(14);"≥     MEMONIX     ≥    OP CODE   ≥"
5480 LPRINT CHR$(27);CHR$(69);CHR$(14);"√ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥"
5490 LC=VAL("&H"+MID$(ADD$,1,4))-8192:LCEND=VAL("&h"+MID$(ADD$,6,4))-8192
5500 MAKOM=LIS(MEMORY(LC)):M$=HEX$(LC+8192)+" "+MEMO$(MAKOM)+" ":IF LENG(MAKOM)=1 THEN GOTO 5520 ELSE IF LENG(MAKOM)=2 THEN H$=HEX$(MEMORY(LC+1)):M$=M$+"#$"+STRING$(2-LEN(H$),"0")+H$:GOTO 5520
5510 H$=HEX$(MEMORY(LC+2)):M$=M$+"$"+STRING$(2-LEN(H$),"0")+H$:H$=HEX$(MEMORY(LC+1)):M$=M$+STRING$(2-LEN(H$),"0")+H$
5520 LPRINT CHR$(27);CHR$(69);CHR$(14);"≥";M$;SPC(17-LEN(M$));"≥";
5530 REM ---------
5540 LPRINT HEX$(LC+8192);": ";:H$=HEX$(MEMORY(LC)):M$=STRING$(2-LEN(H$),"0")+H$+" ":IF LENG(MAKOM)=1 THEN GOTO 5560 ELSE H$=HEX$(MEMORY(LC+1)):M$=M$+STRING$(2-LEN(H$),"0")+H$+" "
5550 IF LENG(MAKOM)=3 THEN H$=HEX$(MEMORY(LC+2)):M$=M$+STRING$(2-LEN(H$),"0")+H$
5560 LPRINT M$;SPC(8-LEN(M$));"≥":LC=LC+LENG(MAKOM):IF LC>LCEND THEN LPRINT CHR$(27);CHR$(69);CHR$(14);"¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ":LPRINT:LPRINT:LPRINT:GOTO 5130 ELSE 5500
5570 END
5580 SH$="":I=0:COLOR 15,0
5590 GOSUB 5230:KEY(12)ON
5600 ON KEY (12) GOSUB 5650
5610 T$="":T$=INKEY$:IF T$<" " AND T$<>CHR$(8) AND T$<>CHR$(27) AND T$<> CHR$(13) THEN 5610 ELSE IF T$=CHR$(27) THEN 5130
5620 IF T$=CHR$(8) THEN GOSUB 5650:GOTO 5610
5630 LOCATE 23,28+I:IF T$=CHR$(13) THEN PRINT SPC(9-LEN(SH$)):GOSUB 5230:RETURN
5640 PRINT T$;:SH$=SH$+T$:I=I+1:IF I=8 THEN GOSUB 5230:RETURN ELSE GOTO 5610
5650 GOSUB 5230:IF I=0 THEN RETURN 5590 ELSE I=I-1:LOCATE 23,28+I:PRINT "_":SH$=LEFT$(SH$,I):RETURN 5590
5660 REM -------------- FILER # ----------
5670 ADD$="    -    ":I=1:COLOR 15,0
5680 GOSUB 5230:KEY(12)ON
5690 ON KEY (12) GOSUB 5740
5700 T$="":T$=INKEY$:IF T$=CHR$(8) OR T$=CHR$(27) OR ((T$>="0" AND T$<="9") OR (T$>="A" AND T$<="F")) THEN 5710 ELSE 5700
5710 IF T$=CHR$(8) THEN GOSUB 5740:GOTO 5700 ELSE IF T$=CHR$(27) THEN 5130
5720 LOCATE 23,30+I:PRINT T$:MID$(ADD$,I,1)=T$:I=I+1:IF I=5 THEN I=6 ELSE IF I=10 THEN RETURN
5730 GOTO 5700
5740 GOSUB 5230:IF I=1 THEN RETURN 5680 ELSE I=I-1:IF I=5 THEN I=4
5750 LOCATE 23,30+I:PRINT "_":SH$=LEFT$(SH$,I):RETURN 5680
5760 REM ----------------------------------------------
5770 LOCATE 4,55:COLOR 0,7:PRINT "çâòÇÖÄ öëîÉÑ":COLOR 7,0
5780 LOCATE  5 ,48 :PRINT "⁄ƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒø"
5790 LOCATE  6 ,48 :PRINT "≥  xxxxxx  F ≥ 00  A òÅÖñ ≥"
5800 LOCATE  7 ,48 :PRINT "√ƒƒƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒ¥"
5810 LOCATE  8 ,48 :PRINT "≥ 00  B òÇÖÄ ≥ 00  C òÇÖÄ ≥"
5820 LOCATE  9 ,48 :PRINT "√ƒƒƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒ¥"
5830 LOCATE  10 ,48 :PRINT "≥ 00  D òÇÖÄ ≥ 00  E òÇÖÄ ≥"
5840 LOCATE  11,48 :PRINT "√ƒƒƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒ¥"
5850 LOCATE  12 ,48 :PRINT "≥ 00  H òÇÖÄ ≥ 00  L òÇÖÄ ≥"
5860 LOCATE  13,48 :PRINT "√ƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒ¥"
5870 LOCATE  14 ,48 :PRINT "≥    38FE   : öâêëáé ÑòÖé ≥"
5880 LOCATE  15 ,48 :PRINT "¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ"
5890 LOCATE 17,57:COLOR 0,7:PRINT "àåî - àåó":COLOR 7,0
5900 LOCATE  18 ,53 :PRINT "⁄ƒ¬ƒ¬ƒ¬ƒ¬ƒ¬ƒ¬ƒ¬ƒø"
5910 LOCATE  19 ,53 :PRINT "≥˛≥˛≥˛≥˛≥˛≥˛≥˛≥˛≥"
5920 LOCATE  20 ,53 :PRINT "√ƒ≈ƒ≈ƒ≈ƒ≈ƒ≈ƒ≈ƒ≈ƒ¥"
5930 LOCATE  21 ,53 :PRINT "≥‹≥‹≥‹≥‹≥‹≥‹≥‹≥‹≥"
5940 LOCATE  22 ,53 :PRINT "¿ƒ¡ƒ¡ƒ¡ƒ¡ƒ¡ƒ¡ƒ¡ƒŸ":RETURN
