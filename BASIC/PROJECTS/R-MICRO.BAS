10 ' ***************************************
20 ' ***** 03:06:1987 ÑêãÖö            *****
30 ' *****   òÖëëÖòî Öòóâé -Ñ âÖéâÉå   *****
40 ' *****         8  0  8  5          *****
50 ' *****     Éåîêââåó èêíò âÉâ åí    *****
60 ' ***** 1987 - Ñêô      9-Äâ - Ñöâã *****
70 ' ***************************************
80 DEF FNX(X,Y)=X*256+Y-8192:DEF FNR(X,Y)=X*256+Y:CLS:DEFINT A-Z:INS$(0)="Insert ":INS$(2)="Replace":SP$=SPACE$(20):KEY OFF:KEY(1)OFF:SP=6398:Y=5:X=2:INS=1:BP=1:SPD$=STRING$(20,205):IP$="CSAOLPGMTVK"
90 DIM MRY(8192),MEM$(255),X(6,8),BAN$(15),CMD$(11),OT$(15),FL$(15),P(15),ER$(15):GOSUB 4770:GOSUB 4230
100 ON ERROR GOTO 110:OPEN "R",#1,"MCRNAME.MBR",26:FIELD #1,8 AS NUMBER$,8 AS FLNM$,4 AS START$,4 AS RANGE$,1 AS X$,1 AS CR$:GET #1,1:EF=VAL(NUMBER$):GOTO 3320
110 IF ERR=71 THEN ER=14 ELSE IF ERR=70 THEN ER=15 ELSE IF ERR=72 THEN ER=9
120 CLS:PRINT ER$(ER-1):SOUND 600,BP:PRINT "Press any key to retry operation":E$=INPUT$(1):CLS:GOSUB 4230:GOTO 100
130 ' ***** Check line number in editor *****
140 IF Y=22 THEN Y=5:RETURN ELSE Y=Y+1:RETURN
150 ' ***** Check flags *****
160 IF X>-256 AND X<256 THEN CY=0:RETURN ELSE CY=1:IF X>255 THEN X=X-256:RETURN ELSE X=X+256:RETURN
170 IF X>255 THEN X=X-256 ELSE IF X<-255 THEN X=X+256
180 Z=1-ABS(SGN(X)):P=P(INT(X/16))+P(X-(INT(X/16))*16):P=1-ABS(SGN(P-2)):IF X>=0 THEN S=0:RETURN ELSE S=1:IF X<-255 THEN X=X+255:RETURN ELSE RETURN
190 ' ***** Check flags routine *****
200 GOSUB 160:GOSUB 170:RA=X:GOTO 290
210 LOCATE 23,3:PRINT " Press any key to continue ":SOUND 600,BP:RETURN
220 LOCATE 23,2:PRINT SPD$;"œ";SPD$:RETURN
230 ' ***** Trace *****
240 GOSUB 210:E$=INPUT$(1):GOSUB 220:LIN=LIN-1:GOSUB 3230:LIN=LIN+8193:IF E$="B" OR E$="b" OR E$="ê" THEN 370 ELSE OP=MRY(LIN-8192):LOCATE 12,59:PRINT SP$:M$=LEFT$(MEM$(OP),VAL(MID$(MEM$(OP),12,1)))+" ":LNG=VAL(RIGHT$(MEM$(OP),1)):B=INSTR(5,MEM$(OP),"$")
250 A$="":B$="":IF LNG>1 THEN M$=M$+MID$(MEM$(OP),5,B-5):A$=HEX$(MRY(LIN-8191)):GOSUB 4030:B$=A$:A$="":IF LNG=3 THEN A$=HEX$(MRY(LIN-8190)):GOSUB 4030
260 M$=M$+A$+B$:IF B$<>"" THEN O$=O$+","+B$
270 IF A$<>"" THEN O$=O$+","+A$:RETURN ELSE RETURN
280 ' ***** Go *****
290 LIN=LIN+VAL(RIGHT$(MEM$(MRY(LIN)),1))
300 A=MRY(LIN+1):B=MRY(LIN+2):IF C$="T" THEN GOSUB 240:LIN=LIN-8192:A=MRY(LIN+1):B=MRY(LIN+2):LOCATE 12,59:PRINT M$ ELSE OP=MRY(LIN):E$=INKEY$:IF E$=" " THEN GOSUB 210:E$=INPUT$(1):GOSUB 220 ELSE IF E$="B" OR E$="b" OR E$="ê" THEN 370
310 ON OP+1 GOTO 290,390,430,2380,2230,2310,460,2500,3080,2460,1450,2420,2240,2320,470,2520,3110,400,440,2390,2250,2330,480,2540,3120,2470,1460,2430,2260,2340,490,2560,3070,410,1490,2400,2270,2350,500,2580,3130,2480,1480,2440,2280,2360,510
320 ON OP-46 GOTO 1410,3050,420,1470,2410,2290,2370,520,1430,3140,2490,1440,2450,2220,2300,450,1420,290,610,620,630,640,650,660,600,680,290,690,700,710,720,730,670,750,760,290,770,780,790,800,740,820,830,840,300,850,860,870,810,890
330 ON OP-96 GOTO 900,910,920,290,930,940,880,960,970,980,990,1000,290,1010,950,1030,1040,1050,1060,1070,1080,1090,1020,530,540,550,560,570,580,590,290,1510,1520,1530,1540,1550,1560,1570,1500,1590,1600,1610,1620,1630,1640,1650
340 ON OP-142 GOTO 1580,1670,1680,1690,1700,1710,1720,1730,1660,1750,1760,1770,1780,1790,1800,1810,1740,1830,1840,1850,1860,1870,1880,1890,1820,1910,1920,1930,1940,1950,1960,1970,1900,1990,2000,2010,2020,2030,2040,2050,1980,2070,2080,2090
350 ON OP-186 GOTO 2100,2110,2120,2130,2060,1290,2910,1110,1100,1200,2910,2140,2600,1300,1280,1120,3150,1210,1190,2150,2610,1310,2960,1130,2720,1220,2920,2160,2620,1320,3160,1140,2870,1230,3170,2170,2630,1330,2970,1150,1380,1240,2930,2180
360 ON OP-230 GOTO 2640,1340,1370,1160,1400,1250,3180,2190,2650,1350,2980,1170,2690,1260,2950,2200,2660,1360,1390,1180,2710,1270,3190,2210,2670
370 A$="Break in addres "+HEX$(LIN+8193):GOTO 3210
380 A$="Error in addres "+HEX$(LIN+8192):GOTO 3210
390 RB=B:RC=A:GOTO 290
400 RD=B:RE=A:GOTO 290
410 RH=B:RL=A:GOTO 290
420 SP=B*256+A-8192:GOTO 290
430 MRY(FNX(RB,RC))=RA:GOTO 290
440 MRY(FNX(RD,RE))=RA:GOTO 290
450 RA=A:GOTO 290
460 RB=A:GOTO 290
470 RC=A:GOTO 290
480 RD=A:GOTO 290
490 RE=A:GOTO 290
500 RH=A:GOTO 290
510 RL=A:GOTO 290
520 MRY(FNX(RH,RL))=A:GOTO 290
530 RA=RB:GOTO 290
540 RA=RC:GOTO 290
550 RA=RD:GOTO 290
560 RA=RE:GOTO 290
570 RA=RH:GOTO 290
580 RA=RL:GOTO 290
590 RA=MRY(FNX(RH,RL)):GOTO 290
600 RB=RA:GOTO 290
610 RB=RC:GOTO 290
620 RB=RD:GOTO 290
630 RB=RE:GOTO 290
640 RB=RH:GOTO 290
650 RB=RL:GOTO 290
660 RB=MRY(FNX(RH,RL)):GOTO 290
670 RC=RA:GOTO 290
680 RC=RB:GOTO 290
690 RC=RD:GOTO 290
700 RC=RE:GOTO 290
710 RC=RH:GOTO 290
720 RC=RL:GOTO 290
730 RC=MRY(FNX(RH,RL)):GOTO 290
740 RD=RA:GOTO 290
750 RD=RB:GOTO 290
760 RD=RC:GOTO 290
770 RD=RE:GOTO 290
780 RD=RH:GOTO 290
790 RD=RL:GOTO 290
800 RD=MRY(FNX(RH,RL)):GOTO 290
810 RE=RA:GOTO 290
820 RE=RB:GOTO 290
830 RE=RC:GOTO 290
840 RE=RD:GOTO 290
850 RE=RH:GOTO 290
860 RE=RL:GOTO 290
870 RE=MRY(FNX(RH,RL)):GOTO 290
880 RH=RA:GOTO 290
890 RH=RB:GOTO 290
900 RH=RC:GOTO 290
910 RH=RD:GOTO 290
920 RH=RE:GOTO 290
930 RH=RL:GOTO 290
940 RH=MRY(FNX(RH,RL)):GOTO 290
950 RL=RA:GOTO 290
960 RL=RB:GOTO 290
970 RL=RC:GOTO 290
980 RL=RD:GOTO 290
990 RL=RE:GOTO 290
1000 RL=RH:GOTO 290
1010 RL=MRY(FNX(RH,RL)):GOTO 290
1020 MRY(FNX(RH,RL))=RA:GOTO 290
1030 MRY(FNX(RH,RL))=RB:GOTO 290
1040 MRY(FNX(RH,RL))=RC:GOTO 290
1050 MRY(FNX(RH,RL))=RD:GOTO 290
1060 MRY(FNX(RH,RL))=RE:GOTO 290
1070 MRY(FNX(RH,RL))=RH:GOTO 290
1080 MRY(FNX(RH,RL))=RL:GOTO 290
1090 A$="Halt at adrres "+HEX$(LIN+8192):GOTO 3210
1100 LIN=B*256+A-8194:GOTO 290
1110 IF Z=0 THEN 1100 ELSE 290
1120 IF Z=1 THEN 1100 ELSE 290
1130 IF CY=0 THEN 1100 ELSE 290
1140 IF CY=1 THEN 1100 ELSE 290
1150 IF P=0 THEN 1100 ELSE 290
1160 IF P=1 THEN 1100 ELSE 290
1170 IF S=0 THEN 1100 ELSE 290
1180 IF S=1 THEN 1100 ELSE 290
1190 SP=SP-1:MRY(SP)=INT((LIN+8195)/256):SP=SP-1:MRY(SP)=LIN+8195-INT((LIN+8195)/256)*256:LIN=256*B+A-8193:GOTO 290
1200 IF Z=0 THEN 1190 ELSE 290
1210 IF Z=1 THEN 1190 ELSE 290
1220 IF CY=0 THEN 1190 ELSE 290
1230 IF CY=1 THEN 1190 ELSE 290
1240 IF P=0 THEN 1190 ELSE 290
1250 IF P=1 THEN 1190 ELSE 290
1260 IF S=0 THEN 1190 ELSE 290
1270 IF S=1 THEN 1190 ELSE 290
1280 LIN=MRY(SP)+MRY(SP+1)*256-8194:SP=SP+2:GOTO 290
1290 IF Z=1 THEN 1280 ELSE 290
1300 IF Z=1 THEN 1280 ELSE 290
1310 IF CY=0 THEN 1280 ELSE 290
1320 IF CY=1 THEN 1280 ELSE 290
1330 IF P=0 THEN 1280 ELSE 290
1340 IF P=1 THEN 1280 ELSE 290
1350 IF S=0 THEN 1280 ELSE 290
1360 IF S=1 THEN 1280 ELSE 290
1370 LIN=FNX(RH,RL):GOTO 290
1380 SWAP RL,MRY(SP):SWAP RH,MRY(SP+1):GOTO 290
1390 SP=FNX(RH,RL):GOTO 290
1400 SWAP RH,RD:SWAP RL,RE:GOTO 290
1410 RA=255-RA:GOTO 290
1420 CY=ABS(CY-1):GOTO 290
1430 CY=1:GOTO 290
1440 RA=MRY(B*256+MRY(SP)-8191):GOTO 290
1450 RA=MRY(FNX(RB,RC)):GOTO 290
1460 RA=MRY(FNX(RD,RE)):GOTO 290
1470 MRY(B*256+A-8191)=RA:GOTO 290
1480 I=B*256+A-8191:RL=MRY(I):RH=MRY(I+1):GOTO 290
1490 I=B*256+A-8191:MRY(I)=RL:MRY(I+1)=RH:GOTO 290
1500 RA=2*RA:X=RA:GOTO 200
1510 RA=RA+RB:X=RA:GOTO 200
1520 RA=RA+RC:X=RA:GOTO 200
1530 RA=RA+RD:X=RA:GOTO 200
1540 RA=RA+RE:X=RA:GOTO 200
1550 RA=RA+RH:X=RA:GOTO 200
1560 RA=RA+RL:X=RA:GOTO 200
1570 RA=RA+MRY(FNX(RH,RL)):X=RA:GOTO 200
1580 RA=2*RA+CY:X=RA:GOTO 200
1590 RA=RA+RB+CY:X=RA:GOTO 200
1600 RA=RA+RC+CY:X=RA:GOTO 200
1610 RA=RA+RD+CY:X=RA:GOTO 200
1620 RA=RA+RE+CY:X=RA:GOTO 200
1630 RA=RA+RH+CY:X=RA:GOTO 200
1640 RA=RA+RL+CY:X=RA:GOTO 200
1650 RA=RA+MRY(FNX(RH,RL))+CY:X=RA:GOTO 200
1660 RA=0:Z=1:CY=0:P=1:S=0:GOTO 290
1670 RA=RA-RB:X=RA:GOTO 200
1680 RA=RA-RC:X=RA:GOTO 200
1690 RA=RA-RD:X=RA:GOTO 200
1700 RA=RA-RE:X=RA:GOTO 200
1710 RA=RA-RH:X=RA:GOTO 200
1720 RA=RA-RL:X=RA:GOTO 200
1730 RA=RA-MRY(FNX(RH,RL)):X=RA:GOTO 200
1740 RA=CY:X=RA:GOTO 200
1750 RA=RA-RA-CY:IF RA<255 THEN RA=RA+256:GOTO 290 ELSE 290
1760 RA=RA-RC-CY:X=RA:GOTO 200
1770 RA=RA-RD-CY:X=RA:GOTO 200
1780 RA=RA-RE-CY:X=RA:GOTO 200
1790 RA=RA-RH-CY:X=RA:GOTO 200
1800 RA=RA-RL-CY:X=RA:GOTO 200
1810 RA=RA-MRY(FNX(RH,RL))-CY:X=RA:GOTO 200
1820 X=RA:GOTO 200
1830 RA=RA AND RB:X=RA:GOTO 200
1840 RA=RA AND RC:X=RA:GOTO 200
1850 RA=RA AND RD:X=RA:GOTO 200
1860 RA=RA AND RE:X=RA:GOTO 200
1870 RA=RA AND RH:X=RA:GOTO 200
1880 RA=RA AND RL:X=RA:GOTO 200
1890 RA=RA AND MRY(FNX(RH,RL)):X=RA:GOTO 200
1900 RA=0:Z=1:CY=0:P=1:S=0:GOTO 290
1910 RA=RA XOR RB:X=RA:GOTO 200
1920 RA=RA XOR RC:X=RA:GOTO 200
1930 RA=RA XOR RD:X=RA:GOTO 200
1940 RA=RA XOR RE:X=RA:GOTO 200
1950 RA=RA XOR RH:X=RA:GOTO 200
1960 RA=RA XOR RL:X=RA:GOTO 200
1970 RA=RA XOR MRY(FNX(RH,RL)):X=RA:GOTO 200
1980 X=RA:GOTO 200
1990 RA=RA OR RB:X=RA:GOTO 200
2000 RA=RA OR RC:X=RA:GOTO 200
2010 RA=RA OR RD:X=RA:GOTO 200
2020 RA=RA OR RE:X=RA:GOTO 200
2030 RA=RA OR RH:X=RA:GOTO 200
2040 RA=RA OR RL:X=RA:GOTO 200
2050 RA=RA OR MRY(FNX(RH,RL)):X=RA:GOTO 200
2060 Z=1:CY=0:P=1:S=0:GOTO 290
2070 X=RA-RB:GOTO 200
2080 X=RA-RC:GOTO 200
2090 X=RA-RD:GOTO 200
2100 X=RA-RE:GOTO 200
2110 X=RA-RH:GOTO 200
2120 X=RA-RL:GOTO 200
2130 X=RA-MRY(FNX(RH,RL)):GOTO 200
2140 RA=RA+A:X=RA:GOTO 200
2150 RA=RA+A+CY:X=RA:GOTO 200
2160 RA=RA-A:X=RA:GOTO 200
2170 RA=RA-A-CY:X=RA:GOTO 200
2180 RA=RA AND A:X=RA:GOTO 200
2190 RA=RA XOR A:X=RA:GOTO 200
2200 RA=RA OR A:X=RA:GOTO 200
2210 X=RA-A:GOSUB 160:GOSUB 170:GOTO 290
2220 RA=RA+1:X=RA:GOSUB 170:RA=X:GOTO 290
2230 RB=RB+1:X=RB:GOSUB 170:RB=X:GOTO 290
2240 RC=RC+1:X=RC:GOSUB 170:RC=X:GOTO 290
2250 RD=RD+1:X=RD:GOSUB 170:RD=X:GOTO 290
2260 RE=RE+1:X=RE:GOSUB 170:RE=X:GOTO 290
2270 RH=RH+1:X=RH:GOSUB 170:RH=X:GOTO 290
2280 RL=RL+1:X=RL:GOSUB 170:RL=X:GOTO 290
2290 I=FNX(RH,RL)-1:MRY(I)=MRY(I)+1:X=MRY(I):GOSUB 170:MRY(I)=X:GOTO 290
2300 RA=RA-1:X=RA:GOSUB 170:RA=X:GOTO 290
2310 RB=RB-1:X=RB:GOSUB 170:RB=X:GOTO 290
2320 RC=RC-1:X=RC:GOSUB 170:RC=X:GOTO 290
2330 RD=RD-1:X=RD:GOSUB 170:RD=X:GOTO 290
2340 RE=RE-1:X=RE:GOSUB 170:RE=X:GOTO 290
2350 RH=RH-1:X=RH:GOSUB 170:RH=X:GOTO 290
2360 RL=RL-1:X=RL:GOSUB 170:RL=X:GOTO 290
2370 I=FNX(RH,RL)-1:MRY(I)=MRY(I)-1:X=MRY(I):GOSUB 170:MRY(I)=X:GOTO 290
2380 X=FNR(RB,RC)+1:RB=INT(X/256):RC=X-RB*256:GOTO 290
2390 X=FNR(RD,RE)+1:RD=INT(X/256):RE=X-RD*256:GOTO 290
2400 X=FNR(RH,RL)+1:RH=INT(X/256):RL=X-RH*256:GOTO 290
2410 SP=SP+1:GOTO 290
2420 X=FNR(RB,RC)-1:RB=INT(X/256):RC=X-RB*256:GOTO 290
2430 X=FNR(RD,RE)-1:RD=INT(X/256):RE=X-RD*256:GOTO 290
2440 X=FNR(RH,RL)-1:RH=INT(X/256):RL=X-RH*256:GOTO 290
2450 SP=SP-1:GOTO 290
2460 X=FNR(RH,RL)+FNR(RB,RC):RH=INT(X/256):RL=X-RH*256:X=RH:GOSUB 160:RH=X:GOTO 290
2470 X=FNR(RH,RL)+FNR(RD,RE):RH=INT(X/256):RL=X-RH*256:X=RH:GOSUB 160:RH=X:GOTO 290
2480 X=2*(FNR(RH,RL)):RH=INT(X/256):RL=X-RH*256:X=RH:GOSUB 160:RH=X:GOTO 290
2490 X=FNR(RH,RL)+SP+8192:RH=INT(X/256):RL=X-RH*256:X=RH:GOSUB 160:RH=X:GOTO 290
2500 CY=RA AND 1:IF RA>127 THEN X=1 ELSE X=0
2510 RA=RA*2+X:IF RA>255 THEN RA=RA-256:GOTO 290 ELSE 290
2520 X=RA AND 1:IF RA>127 THEN CY=1 ELSE CY=0
2530 RA=INT(RA/2):RA=RA+128*X:GOTO 290
2540 X=CY:IF RA>127 THEN CY=1 ELSE CY=0
2550 RA=RA*2+X:IF RA>255 THEN RA=RA-256:GOTO 290 ELSE 290
2560 X=RA AND 1:RA=INT(RA/2):RA=RA+128*CY:CY=X:GOTO 290
2570 ' ***** DAA *****
2580 X=RA:GOTO 200
2590 ' ***** RST * *****
2600 GOTO 290
2610 GOTO 290
2620 GOTO 290
2630 GOTO 290
2640 GOTO 290
2650 GOTO 290
2660 GOTO 290
2670 GOTO 290
2680 ' ***** DI *****
2690 GOTO 290
2700 ' ***** EI *****
2710 GOTO 290
2720 IF A<>0 THEN 2730 ELSE X$=RIGHT$(HEX$(RA),2):X$=STRING$(2-LEN(X$),"0")+X$:A=VAL("&h"+LEFT$(X$,1)):B=VAL("&h"+RIGHT$(X$,1)):LOCATE 15,57:PRINT OT$(A);"∫";OT$(B):GOTO 290
2730 IF A<>4 THEN 2830 ELSE IF IN5<1 OR IN5>6 THEN 380
2740 X=RA:IF X<128 THEN P$(1)="ƒ" ELSE P$(1)=" ":X=X-128
2750 IF X<64 THEN P$(2)="≥" ELSE P$(2)=" ":X=X-64
2760 IF X<32 THEN P$(3)="≥" ELSE P$(3)=" ":X=X-32
2770 IF X<16 THEN P$(4)="ƒ" ELSE P$(4)=" ":X=X-16
2780 IF X<8 THEN P$(5)="." ELSE P$(5)=" ":X=X-8
2790 IF X<4 THEN P$(6)="ƒ" ELSE P$(6)=" ":X=X-4
2800 IF X<2 THEN P$(7)="≥" ELSE P$(7)=" ":X=X-2
2810 IF X<1 THEN P$(8)="≥" ELSE P$(8)=" "
2820 FOR I=1 TO 8:LOCATE Y(I),X(IN5,I)-1:PRINT P$(I):NEXT:IF INK=0 THEN 290 ELSE IN5=IN5+1:IF IN5<1 OR IN5>6 THEN IN5=1:GOTO 290 ELSE 290
2830 IF A<>5 THEN 2860 ELSE S=RA:A=INT(S/16):B=S-A*16:IF A<>8 THEN 380
2840 IF B>7 THEN INK=1:B=B-8 ELSE INK=0
2850 IN5=B+1:IF IN5>6 THEN IN5=6:GOTO 290 ELSE 290
2860 IF (RA=7 OR (RA=>9 AND RA<=13) OR (RA=>28 AND RA<=31)) THEN 380 ELSE LOCATE 13,55+FX:PRINT CHR$(RA):IF FX=20 THEN FX=1:GOTO 290 ELSE FX=FX+1:GOTO 290
2870 IF A<>0 THEN 2900 ELSE LOCATE 17,57:PRINT "1 2 3 4 5 6 7 8":GOSUB 140:LOCATE Y,2:PRINT "Enter number in #00 ≥";SP$
2880 E$=INPUT$(1):IF E$=CHR$(13) THEN 2890 ELSE IF E$<"1" OR E$>"8" THEN 2880 ELSE I=ASC(E$)-48:LOCATE 16,55+I*2:IF A(I)=0 THEN A(I)=1:PRINT "ﬂ":GOTO 2880 ELSE A(I)=0:PRINT "‹":GOTO 2880
2890 RA=0:FOR I=1 TO 8:RA=RA+2^(I-1)*A(9-I):NEXT:LOCATE 17,57:PRINT "Õ Õ Õ Õ Õ Õ Õ Õ":GOTO 290
2900 GOSUB 140:LOCATE Y,2:PRINT "Enter number in #FF ≥";SP$:E$=INPUT$(1):A=ASC(E$):IF A=7 OR A=>9 AND A<=13 OR A=>28 AND A<=31 THEN GOTO 2900 ELSE RA=A:LOCATE 13,55+FX:PRINT CHR$(RA):IF FX=20 THEN FX=1:GOTO 290 ELSE FX=FX+1:GOTO 290
2910 SP=SP-1:MRY(SP)=RB:SP=SP-1:MRY(SP)=RC:GOTO 290
2920 SP=SP-1:MRY(SP)=RD:SP=SP-1:MRY(SP)=RE:GOTO 290
2930 SP=SP-1:MRY(SP)=RH:SP=SP-1:MRY(SP)=RL:GOTO 290
2940 GOSUB 4170:SP=SP-1:MRY(SP)=RA:SP=SP-1:MRY(SP)=A:GOTO 290
2950 RC=MRY(SP):RB=MRY(SP+1):SP=SP+2:GOTO 290
2960 RE=MRY(SP):RD=MRY(SP+1):SP=SP+2:GOTO 290
2970 RL=MRY(SP):RH=MRY(SP+1):SP=SP+2:GOTO 290
2980 A=MRY(SP):RA=MRY(SP+1):SP=SP+2:IF A>127 THEN S=1:A=A-128 ELSE S=0
2990 IF A>63 THEN Z=1:A=A-64 ELSE Z=0
3000 A=A-INT(A/16)*16:IF A>4 THEN A=A-4
3010 IF A>2 THEN P=1:A=A-2 ELSE P=0
3020 IF A>1 THEN A=A-1
3030 CY=A:GOTO 290
3040 ' ***** SIM *****
3050 GOTO 290
3060 ' ***** RIM *****
3070 GOTO 290
3080 IF A=>60 THEN A=A-1:GOTO 3080 ELSE TIME$="00"
3090 E$=INKEY$:IF E$="B" OR E$="b" OR E$="ê" THEN 370 ELSE IF VAL(RIGHT$(TIME$,2))=A THEN 290 ELSE 3090
3100 ' ***** Empty * *****
3110 GOTO 290
3120 GOTO 290
3130 GOTO 290
3140 GOTO 290
3150 GOTO 290
3160 GOTO 290
3170 GOTO 290
3180 GOTO 290
3190 GOTO 290
3200 ' ***** Halt, Break or Error *****
3210 X=2:SOUND 600,BP:GOSUB 140:GOSUB 4100:LOCATE Y,2:PRINT A$:GOSUB 140:GOSUB 4100:GOSUB 3220:GOTO 3320
3220 ' ***** Correcting registers after GO *****
3230 A$=HEX$(RA):GOSUB 4020:LOCATE 5,53:PRINT A$:LOCATE 5,67:PRINT USING "+###";RA:A$=HEX$(RA):GOSUB 4030:A=VAL("&h"+LEFT$(A$,1)):B=VAL("&h"+RIGHT$(A$,1)):LOCATE 5,57:PRINT BAN$(A);BAN$(B):A$=HEX$(RB):GOSUB 4020
3240 LOCATE 6,53:PRINT A$:A$=HEX$(RC):GOSUB 4020:LOCATE 6,63:PRINT A$:A$=HEX$(RD):GOSUB 4020:LOCATE 7,53:PRINT A$:A$=HEX$(RE):GOSUB 4020:LOCATE 7,63:PRINT A$:A$=HEX$(RH):GOSUB 4020:LOCATE 8,53:PRINT A$:A$=HEX$(RL):GOSUB 4020:LOCATE 8,63
3250 PRINT A$:A$=HEX$(SP+8192):GOSUB 4040:LOCATE 9,53:PRINT A$:LOCATE 10,53:PRINT HEX$(LIN+8193):A$=RIGHT$(STR$(S),1)+RIGHT$(STR$(Z),1)+RIGHT$(STR$(P),1)+RIGHT$(STR$(CY),1):A=0:FOR I=0 TO 3:A=A+2^I*VAL(MID$(A$,4-I,1)):NEXT:LOCATE 3,66:PRINT FL$(A)
3260 A$=HEX$(MRY(LIN+1)):GOSUB 4020:LOCATE 10,75:PRINT A$:IF RB<32 OR RB>63 THEN A$="--" ELSE A$=HEX$(MRY(FNX(RB,RC))):GOSUB 4030
3270 LOCATE 6,75:PRINT A$:IF RD<32 OR RD>63 THEN A$="--" ELSE A$=HEX$(MRY(FNX(RD,RE))):GOSUB 4030
3280 LOCATE 7,75:PRINT A$:IF RH<32 OR RH>63 THEN A$="--" ELSE A$=HEX$(MRY(FNX(RH,RL))):GOSUB 4030
3290 LOCATE 8,75:PRINT A$:GOSUB 4170:GOSUB 4040:LOCATE 11,53:PRINT A$:ON ERROR GOTO 4050:RETURN
3300 ' CLS:PRINT ERR;ERL:END ' RESUME NEXT
3310 ' ***** Editor *****
3320 IF X>21 THEN X=21 ELSE IF X<1 THEN X=2
3330 IF X=3 THEN GOSUB 4000
3340 CRS$=CHR$(SCREEN(Y,X)):IF INS=1 THEN COLOR 1,0 ELSE COLOR 0,1
3350 LOCATE Y,X,0:PRINT CRS$:COLOR 3,0:LOCATE 3,8:PRINT USING"##";Y-4:LOCATE 3,20:PRINT USING"##";X-1
3360 E$=INKEY$:IF E$=""THEN 3360 ELSE LOCATE Y,X:PRINT CRS$:IF E$=" "OR E$="-"OR E$="*"OR E$="#"OR E$=","OR E$=>"0"AND E$<="9"OR E$=>"A"AND E$<="Z"THEN 3460
3370 IF ASC(E$)=0 THEN A$=RIGHT$(E$,1):GOTO 3390 ELSE IF E$=CHR$(13) THEN INS=1:GOTO 3540 ELSE IF E$=CHR$(8) THEN IF X=2 THEN 3320 ELSE FOR I=X TO 21:A=SCREEN(Y,I):LOCATE Y,I-1:PRINT CHR$(A);:NEXT I:X=X-1:PRINT " ":GOTO 3320
3380 IF E$=CHR$(27) THEN LOCATE Y,2:A$=SPACE$(20):PRINT A$;"≥";A$:X=2:GOTO 3320
3390 IF A$="R" THEN INS=INS*-1:LOCATE 3,25:PRINT INS$(INS+1):GOTO 3320 ELSE IF A$="M" THEN IF X=21 THEN X=21:GOTO 3340 ELSE X=X+1:GOTO 3320
3400 IF A$="D" THEN GOSUB 4190:GOTO 4280 ELSE IF A$="K" THEN IF X=2 THEN X=2:GOTO 3340 ELSE X=X-1:GOTO 3320
3410 IF A$="H" THEN IF Y=5 THEN Y=5:GOTO 3340 ELSE Y=Y-1:GOTO 3320
3420 IF A$="P" THEN IF Y=22 THEN Y=22:GOTO 3340 ELSE Y=Y+1:GOTO 3320
3430 IF A$="w" THEN Y=5:X=2:GOTO 3320 ELSE IF A$="u" THEN LOCATE Y,X:PRINT STRING$(22-X,32):GOTO 3320 ELSE IF A$="S" THEN FOR I=X+1 TO 21:A$=CHR$(SCREEN(Y,I)):LOCATE Y,I-1:PRINT A$:NEXT I:LOCATE Y,21:PRINT " ":GOTO 3320
3440 IF A$="O" THEN FOR I=21 TO 2 STEP-1:IF SCREEN(Y,I)<>32 THEN X=I+1:GOTO 3320 ELSE NEXT:X=2:GOTO 3320
3450 IF A$="A" THEN RSET NUMBER$=STR$(EF):PUT #1,1:CLS:END ELSE IF A$="G" THEN X=2:GOTO 3320 ELSE IF A$="t" THEN GOSUB 3480:GOTO 3320 ELSE IF A$="s" THEN GOSUB 3510:GOTO 3320 ELSE 3340
3460 IF INS=1 THEN LOCATE Y,X:PRINT E$:X=X+1:GOTO 3320 ELSE FOR I=21 TO X STEP-1:LOCATE Y,I:PRINT CHR$(SCREEN(Y,I-1)):NEXT:LOCATE Y,X:PRINT E$:X=X+1:GOTO 3320
3470 ' ***** Next word *****
3480 FOR I=X TO 21:C$=CHR$(SCREEN(Y,I)):IF C$=" " OR C$="-" OR C$="," OR C$="#" OR C$=":" THEN A=I:A$=C$ ELSE NEXT:RETURN
3490 FOR I=A+1 TO 21:C$=CHR$(SCREEN(Y,I)):IF C$<>A$ THEN X=I:RETURN ELSE NEXT:RETURN
3500 ' ***** Previous word *****
3510 FOR I=X TO 2 STEP -1:C$=CHR$(SCREEN(Y,I)):IF C$=" "OR C$="#"OR C$="-"OR C$=","OR C$=":"THEN A=I:A$=C$ ELSE NEXT:RETURN
3520 FOR I=A-1 TO 2 STEP -1:C$=CHR$(SCREEN(Y,I)):IF C$<>A$ THEN A=I ELSE NEXT:RETURN
3530 FOR I=A-1 TO 1 STEP -1:C$=CHR$(SCREEN(Y,I)):IF C$=" "OR C$="-"OR C$=","OR C$="#"OR C$="∫"OR C$=":"THEN X=I+1:RETURN ELSE NEXT:RETURN
3540 ' ***** Enter *****
3550 LOCATE 3,25:PRINT INS$(2):Z$="∫":FOR I=2 TO 21:Z$=Z$+CHR$(SCREEN(Y,I)):NEXT:GOSUB 220:C$=MID$(Z$,2,1):IF INSTR(IP$,C$)=0 THEN ER=6:GOSUB 4070:GOTO 3320
3560 IF C$="C"THEN 3600 ELSE A=3:B=6:GOSUB 3620:IF C$="P"THEN ON ERROR GOTO 4080:GOSUB 3870:GOTO 3320 ELSE IF C$="L"OR C$="K"THEN GOSUB 3870:GOTO 3320 ELSE IF C$="G"OR C$="T"THEN X=2:LIN=LIN-8192:ON ERROR GOTO 4050:GOTO 300
3570 IF C$="M"THEN GOSUB 4120:GOTO 3320 ELSE IF C$="V"THEN GOSUB 4140:GOTO 3320 ELSE IF MID$(Z$,8,1)=" "THEN GOSUB 140:X=2:GOSUB 4100:GOTO 3320 ELSE GOSUB 3930
3580 IF C$="O"THEN GOSUB 3770:GOSUB 3800:GOSUB 3710:GOTO 3320 ELSE IF C$="A"THEN GOSUB 3650:GOSUB 3740:GOSUB 3710:GOTO 3320 ELSE IF C$="S"THEN GOSUB 3960:GOTO 3330
3590 ' ***** Cls *****
3600 X=2:Y=5:FOR I=1 TO 9:LOCATE 4+I,2:PRINT SP$;"≥";SP$:LOCATE 13+I,2:PRINT SP$;"≥";SP$:NEXT:GOTO 3320
3610 ' ***** Check line number *****
3620 A=VAL("&h"+MID$(Z$,A,B)):IF A<8192 OR A>16383 THEN ER=5:GOSUB 4070:RETURN 3330 ELSE LIN=A:RETURN
3630 ' ***** Hexadecimal to Decimal *****
3640 A=VAL("&h"+A$):RETURN
3650 ' ***** Find mnemonics *****
3660 OP=0:A=1:FOR I=7 TO 0 STEP -1:OP=OP+2^I*A:IF OP>255 THEN OP=255
3670 B$="":B=VAL(MID$(MEM$(OP),14,3)):B$=LEFT$(MEM$(B),VAL(MID$(MEM$(B),12,1)))+" "+MID$(MEM$(B),5,VAL(MID$(MEM$(B),13,1))):D$=LEFT$((CMDA$+" "+LSDA$),LEN(B$)):IF B$=D$ THEN OP=B:GOTO 3690 ELSE IF B$<D$ THEN A=1 ELSE A=-1
3680 NEXT:ER=2:GOSUB 4070:GOTO 3320
3690 W=VAL(RIGHT$(MEM$(OP),1)):IF W=>2 THEN Q=INSTR(5,MEM$(OP),"$"):IF Q<>0 THEN D$=MID$(LSD$,Q-4,W*2-2) ELSE RETURN
3700 LNG=VAL(RIGHT$(MEM$(OP),1)):A$=HEX$(OP):GOSUB 3640:OP=A:MRY(LIN-8192)=OP:IF LNG=1 THEN RETURN ELSE A$=LEFT$(D$,2):GOSUB 3640:IF LNG=2 THEN MRY(LIN-8191)=A:RETURN ELSE MRY(LIN-8190)=A:A$=RIGHT$(D$,2):GOSUB 3640:MRY(LIN-8191)=A:RETURN
3710 ' ***** Write next line number *****
3720 GOSUB 140:IF MID$(Z$,7,1)=" " THEN A$=SP$ ELSE A$=""
3730 LOCATE Y,2:PRINT A$;"≥";A$:LOCATE Y,2:PRINT C$;HEX$(LIN):X=8:RETURN
3740 ' ***** Write op code (s) <gotten out from mnemonics> *****
3750 LOCATE Y,2:PRINT SP$;"≥";SP$:LOCATE Y,2:PRINT C$;HEX$(LIN);":";CMD$;" ";LSD$:LOCATE Y,23:PRINT "O";HEX$(LIN);":";:A=VAL(RIGHT$(MEM$(OP),1))-1:FOR I=LIN TO LIN+A:A$=HEX$(MRY(I-8192)):GOSUB 4020:PRINT A$;:IF I<>LIN+A THEN PRINT ",";
3760 NEXT:LIN=LIN+VAL(RIGHT$(MEM$(OP),1)):RETURN
3770 ' ***** Check command for Op code mode *****
3780 B=0:CRS$="":B$="":FOR I=8 TO 21:A$=MID$(Z$,I,1):IF A$<":" AND A$>"/" OR A$<"G" AND A$>"@" THEN B=1:IF A$=" " OR A$="," THEN 3790 ELSE B$=B$+A$
3790 IF B=0 THEN ER=7:GOSUB 4070:GOTO 3320 ELSE NEXT:RETURN
3800 ' ***** Build mnemonics from op codes *****
3810 OP=VAL("&h"+LEFT$(B$,2)):MRY(LIN-8192)=OP:CMD$=LEFT$(MEM$(OP),4):LSD$=MID$(MEM$(OP),5,7):LNG=VAL(RIGHT$(MEM$(OP),1))
3820 IF LNG=0 THEN ER=8:GOSUB 4070:GOTO 3320 ELSE IF LNG=1 THEN 3850 ELSE IF LNG=2 THEN IF LEN(B$)<>4 THEN ER=7:GOSUB 4070:GOTO 3320 ELSE CRS$=RIGHT$(B$,2):GOSUB 3630:MRY(LIN-8191)=A:A=2
3830 IF LNG=3 THEN IF LEN(B$)<>6 THEN ER=7:GOSUB 4070:GOTO 3320 ELSE A$=RIGHT$(B$,2):CRS$=A$:GOSUB 3630:MRY(LIN-8190)=A:A$=MID$(B$,3,2):CRS$=CRS$+A$:GOSUB 3630:MRY(LIN-8191)=A:A=4
3840 X=INSTR(LSD$,"$"):IF X<>0 THEN MID$(LSD$,X,A)=CRS$
3850 GOSUB 4100:LNG=VAL(RIGHT$(MEM$(OP),1)):A$=HEX$(OP):GOSUB 4030:D$=A$:IF LNG=2 THEN D$=D$+","+RIGHT$(B$,2) ELSE IF LNG=3 THEN D$=D$+","+MID$(B$,3,2)+","+RIGHT$(B$,2)
3860 LOCATE Y,2:PRINT "O";HEX$(LIN);":";D$:LOCATE Y,23:PRINT "A";HEX$(LIN);":";CMD$;" ";LSD$:LIN=LIN+LNG:RETURN
3870 ' ***** List of command from 'line to line' *****
3880 C=LIN:A$=MID$(Z$,7,1):IF (A$="-"OR A$=",") AND MID$(Z$,8,4)="    "THEN F=16383 ELSE IF MID$(Z$,7,2)="  "THEN F=LIN ELSE IF (A$="-"OR A$=",") AND MID$(Z$,8,1)<>" "THEN A=8:B=11:GOSUB 3620:F=LIN
3890 IF F<LIN THEN ER=1:GOSUB 4070:RETURN 3320
3900 LIN=C:OP=MRY(LIN-8192):M$="A"+HEX$(LIN)+":"+LEFT$(MEM$(OP),VAL(MID$(MEM$(OP),12,1)))+" ":O$="O"+HEX$(LIN)+":":A$=HEX$(OP):GOSUB 4030:O$=O$+A$:LNG=VAL(RIGHT$(MEM$(OP),1)):B=INSTR(5,MEM$(OP),"$"):GOSUB 250:IF C$="K"THEN SWAP M$,O$
3910 IF C$<>"P"THEN GOSUB 140:LOCATE Y,2:PRINT SP$;"≥";SP$:LOCATE Y,2:PRINT M$:LOCATE Y,23:PRINT O$ ELSE CMD$=MID$(M$,2,LEN(M$)-1):LPRINT SPC(2);CMD$;SPC(20-LEN(CMD$));MID$(O$,7,LEN(O$)-6)
3920 E$=INKEY$:IF E$<>""OR LIN=>F THEN X=2:GOSUB 140:LOCATE Y,2:PRINT SP$;"≥";SP$:RETURN ELSE C=LIN+LNG:GOTO 3900
3930 ' ***** Check CMD$ & LSD$ *****
3940 A$=LEFT$(Z$,7):CMD$="":LSD$="":FOR I=8 TO 11:E$=MID$(Z$,I,1):IF E$<>" "THEN CMD$=CMD$+E$:NEXT:CMDA$=CMD$:O=I ELSE CMDA$=CMD$:CMD$=CMD$+SPACE$(12-I):A$=A$+CMD$:O=I
3950 FOR I=O TO 21:IF MID$(Z$,I,1)=" "THEN NEXT:LSD$="       ":RETURN ELSE A$=A$+" ":O=I:FOR I=O TO O+7:E$=MID$(Z$,I,1):IF E$=" " THEN LSDA$=LSD$:RETURN ELSE LSD$=LSD$+E$:NEXT:LSDA$=LSD$:RETURN
3960 ' ***** Set memory *****
3970 D$="":A$=RIGHT$(Z$,14):FOR I=1 TO LEN(A$):C$=MID$(A$,I,1):IF C$<>" "AND C$<>","THEN D$=D$+C$
3980 NEXT:IF D$=""THEN ER=3:GOSUB 4070:GOTO 3320 ELSE IF LEN(D$)/2<>INT(LEN(D$)/2) THEN ER=3:GOSUB 4070:GOTO 3320
3990 B=0:FOR I=1 TO LEN(D$) STEP 2:A=VAL("&h"+MID$(D$,I,2)):MRY(LIN-8192+B)=A:B=B+1:NEXT:GOSUB 4100:LOCATE Y,2:PRINT "S";HEX$(LIN);":";D$:GOSUB 140:X=8:LOCATE Y,2:LIN=LIN+B:PRINT "S";HEX$(LIN);":":GOTO 3320
4000 ' ***** Inputs pointing *****
4010 C=SCREEN(Y,3):IF C<>32 AND C<>48 THEN RETURN ELSE C$=CHR$(SCREEN(Y,2)):B=INSTR(IP$,C$):IF B=0 THEN RETURN ELSE LOCATE Y,2:PRINT SP$;"≥";SP$:LOCATE Y,2:PRINT C$;CMD$(B):RETURN
4020 ' ***** Add 0 before string$ - A$ *****
4030 A$=RIGHT$(A$,2):A$=STRING$(2-LEN(A$),"0")+A$:RETURN
4040 A$=RIGHT$(A$,4):A$=STRING$(4-LEN(A$),"0")+A$:RETURN
4050 ER=10:RESUME 3320
4060 ' ***** Errors messages *****
4070 LOCATE 23,2:PRINT SPD$;"œ";SPD$:LOCATE 23,3:PRINT " ";ER$(ER);" !!! ":SOUND 600,BP:RETURN
4080 END
4090 ' ***** Clear full line *****
4100 LOCATE Y,2:PRINT SP$;"≥";SP$:RETURN
4110 ' ***** Move block of memory *****
4120 G=LIN:A=8:B=11:GOSUB 3620:F=LIN:A=16:B=19:GOSUB 3620:T=LIN:WHILE G<>F:MRY(T-8192)=MRY(G-8192):T=T+1:G=G+1:WEND:X=2:GOSUB 140:PRINT Y,2:PRINT SP$:RETURN
4130 ' ***** Set to a value *****
4140 G=LIN:A=8:B=11:GOSUB 3620:F=LIN:V=VAL("&h"+MID$(Z$,13,14)):T=VAL("&h"+MID$(Z$,17,18))
4150 MRY(G-8192)=V:IF G=F OR G=16383 THEN X=2:GOSUB 140:GOSUB 4100:RETURN ELSE G=G+1:V=V+T:IF V>255 THEN V=V-256:GOTO 4150 ELSE GOTO 4150
4160 ' ***** Psw *****
4170 A$=RIGHT$(STR$(S),1)+RIGHT$(STR$(Z),1)+"000"+RIGHT$(STR$(P),1)+"0"+RIGHT$(STR$(CY),1):A=0:FOR I=0 TO 7:A=A+2^I*VAL(MID$(A$,8-I,1)):NEXT:A$=HEX$(A):GOSUB 4030:A$=HEX$(RA)+A$:RETURN
4180 ' ***** Window's *****
4190 COLOR 11,0:LOCATE 3,44:PRINT "⁄";STRING$(33,196);"ø":FOR I=4 TO 10:LOCATE I,44:PRINT "≥";SPC(33);"≥":NEXT:LOCATE 11,44:PRINT "¿";STRING$(33,196);"Ÿ":COLOR 3,0:RETURN
4200 LOCATE 3,44:PRINT "     Flag registers -";SPC(14):LOCATE 4,44:PRINT SPC(35):LOCATE 5,44:PRINT "  AC  - h   b         d";SPC(12):LOCATE 6,44:PRINT "  B   - h     C - h       M - h    ":LOCATE 7,44:PRINT "  D   - h     E - h       M - h    "
4210 LOCATE 8,44:PRINT "  H   - h     L - h       M - h    ":LOCATE 9,44:PRINT "  SP  - h    ";SPC(22);"":LOCATE 10,44:PRINT "  PC  - h";SPC(17);"M - h    ":LOCATE 11,44:PRINT "  PSW - h    ";SPC(22):LIN=LIN-1:GOSUB 3230:LIN=LIN+1:RETURN
4220 ' ***** Main screen *****
4230 RESTORE 4230:CLS:LOCATE 25,2:FOR I=1 TO 8:READ A$,B$:KEY I,"":PRINT " ";A$;:COLOR 0,1:PRINT B$;:COLOR 3,0:NEXT:LOCATE 1,1,0:DATA 1-,Help,2-,Orders,3-,Display,4-,Errors,5-,Operator,7-,Quit,8-,Calculator,10-,In\Out:KEY 9,"":KEY 10,""
4240 PRINT"By Raanan Kleinfeld";SPC(26);"1987 (c) Copy rights reserved MADA":LOCATE 2:PRINT"…";SPD$;SPD$;"ÕÀ";STRING$(35,205);"ª":FOR I=3 TO 22:LOCATE I:PRINT"∫";SP$;"≥";SP$;"∫";SPC(35);"∫":NEXT:LOCATE 23:PRINT"»";SPD$;"œ";SPD$;" ";STRING$(35,205);
4250 PRINT"º":LOCATE 3,3:PRINT"Line  1 - Column  1 - Replace  Ver 1.00":LOCATE 4,1:PRINT"Ã";SPD$;"—";SPD$;"π":LOCATE 12,45:PRINT"Keyboard":LOCATE 13,46:PRINT"#FF ->  [";SP$;"]":LOCATE 14,45:PRINT"Binaric    …ÕÀÕÀÕÀÕÀÕÀÕÀÕÀÕªOn Off":LOCATE 15,46
4260 PRINT"Image Out-∫˘∫˘∫˘∫˘∫˘∫˘∫˘∫˘∫˛   ˘":LOCATE 16,47:PRINT"#00  In -∫‹∫‹∫‹∫‹∫‹∫‹∫‹∫‹∫ﬂ   ‹":LOCATE 17,56:PRINT"»Õ Õ Õ Õ Õ Õ Õ Õº":LOCATE 18,45:PRINT"Digital":LOCATE 20,46:PRINT"Image":LOCATE 22,45:PRINT"#04,#05":GOSUB 4200:RETURN
4270 ' ***** In - Out *****
4280 LOCATE 4,54:PRINT "Input - Output":LOCATE 6,47:PRINT "Load file":LOCATE 7,47:PRINT "Save file":LOCATE 8,47:PRINT "Directory":LOCATE 9,47:PRINT "Delete file":LOCATE 10,51:PRINT "Down, Up, Enter or F7":C=1:LOCATE 6,45:COLOR 11:PRINT "Ø":COLOR 3
4290 E$=INKEY$:IF E$=""THEN 4290 ELSE A=C:IF ASC(E$)=0 THEN A$=RIGHT$(E$,1):IF A$="H"THEN C=C-1 ELSE IF A$="P"THEN C=C+1 ELSE IF A$="A"THEN GOSUB 4200:GOTO 3320
4300 IF E$=CHR$(13) THEN ON C GOTO 4550,4430,4340,4640
4310 COLOR 11:LOCATE A+5,45:PRINT " ":COLOR 3:IF C=0 THEN C=4 ELSE IF C=5 THEN C=1
4320 COLOR 11:LOCATE C+5,45:PRINT "Ø":COLOR 3:GOTO 4290
4330 ' ***** Directory *****
4340 GOSUB 4190:LOCATE 3,57:PRINT "Directory":LOCATE 4,46:PRINT "File name   Start Add.   Range":LOCATE 10,46:PRINT "Page up, Page down, Enter or F7":I=1:J=1
4350 GET #1,1:IF X$="0" AND EF=1 OR EF=0 THEN LOCATE 6,56:PRINT "No files":GOSUB 210:E$=INPUT$(1):GOSUB 220:GOSUB 4190:GOTO 4280
4360 LOCATE J+4,46:GET #1,I:IF X$="0" THEN I=I+1 ELSE COLOR 11,0:PRINT USING "\      \";FLNM$;:PRINT USING "      _ \  \";HEX$(VAL(START$));:PRINT USING "      _ \  \";HEX$(VAL(RANGE$)):COLOR 3,0
4370 IF I=EF THEN FOR H=J+1 TO 5:LOCATE H+4,45:PRINT SPC(33):NEXT:GOTO 4390 ELSE I=I+1:J=J+1:IF J<6 THEN 4360
4380 IF I>EF THEN I=I-1:GOTO 4360
4390 E$=INKEY$:IF E$="" THEN 4390 ELSE A$=RIGHT$(E$,1):IF ASC(E$)=0 THEN IF A$="I" THEN I=I-4-J ELSE IF A$="Q" THEN J=1:GOTO 4360 ELSE IF A$="A" THEN GOSUB 4190:GOTO 4280 ELSE 4390
4400 IF I<1 THEN I=1 ELSE IF I>EF THEN I=I-5
4410 J=1:GOTO 4360
4420 ' ***** Save *****
4430 A=0:ON ERROR GOTO 4470:GOSUB 4190:LOCATE 4,59:PRINT "Save":LOCATE 6,51:PRINT "File name : ________":LOCATE 8,51:PRINT "Range     : ____-____":LOCATE 10,53:PRINT "Enter, <- or Esc":FL$="":ST$="":ED$=""
4440 Q=63:W=6:DCR$="_":LNG=8:A$=FL$:GOSUB 4710:FL$=A$:IF E$=CHR$(27) THEN GOSUB 4190:GOTO 4280
4450 Q=63:W=8:LNG=4:A$=ST$:GOSUB 4710:ST$=A$:ST=VAL("&h"+A$):IF E$=CHR$(27) THEN 4440 ELSE IF ST<8192 OR ST>16383 THEN ER=5:GOSUB 4070:GOTO 4450
4460 Q=68:W=8:LNG=4:A$=ED$:GOSUB 4710:ED$=A$:ED=VAL("&h"+A$):IF E$=CHR$(27) THEN 4450 ELSE IF ED<8192 OR ED>16383 OR ED<ST THEN ER=5:GOSUB 4070:GOTO 4460 ELSE 4490
4470 IF ERL=4450 OR ERL=4460 THEN RESUME NEXT ELSE IF ERR=61 AND (ERL=4490 OR ERL=4530) THEN ER=13:GOSUB 4070:GOTO 4430 ELSE IF ERR=71 THEN ER=14:GOSUB 4070:GOTO 4430 ELSE IF ERR=70 THEN ER=15:GOSUB 4070:GOTO 4430
4480 IF ERR=72 THEN ER=9:GOSUB 4070:GOSUB 4190:GOTO 4430
4490 OPEN FL$+".MCR" FOR OUTPUT AS #2 LEN=4:PRINT #2,ST:PRINT #2,ED:IF ED=ST THEN ED=ED+1
4500 FOR I=ST TO ED:OP=MRY(I-8192):PRINT #2,OP:NEXT I:CLOSE #2
4510 FOR I=1 TO EF:GET #1,I:IF X$="0" THEN 4520 ELSE IF FLNM$=FL$+SPACE$(8-LEN(FL$)) THEN 4530
4520 NEXT:EF=EF+1
4530 LSET CR$=CHR$(13):LSET FLNM$=FL$:RSET START$=RIGHT$(STR$(ST),LEN(STR$(ST))-1):RSET RANGE$=RIGHT$(STR$(ED-ST),LEN(STR$(ED-ST))-1):RSET X$="1":PUT #1,I:GOSUB 4190:GOTO 4280
4540 ' ***** Load *****
4550 A=0:ON ERROR GOTO 4620:GOSUB 4190:LOCATE 4,59:PRINT "Load":LOCATE 6,51:PRINT "File name   : ________":LOCATE 8,51:PRINT "From (    ) : ____":LOCATE 10,53:PRINT "Enter, <- or Esc":FL$="":ST$=""
4560 Q=65:W=6:DCR$="_":LNG=8:A$=FL$:GOSUB 4710:FL$=A$:IF E$=CHR$(27) THEN GOSUB 4190:GOTO 4280
4570 FOR I=1 TO EF:GET #1,I:IF X$="0" THEN 4580 ELSE IF FLNM$=FL$+SPACE$(8-LEN(FL$)) THEN LOCATE 8,57:PRINT USING "\  \";HEX$(VAL(START$)):GOTO 4590
4580 NEXT:FL$="":ER=12:GOSUB 4070:GOTO 4560
4590 LOCATE 8,65:COLOR 11:PRINT "____":COLOR 3:Q=65:W=8:LNG=4:A$=ST$:A=1:GOSUB 4710:A=0:ST$=A$:ST=VAL("&h"+A$):IF E$=CHR$(13) AND ST$="" THEN ST=VAL(START$) ELSE IF E$=CHR$(27) THEN 4560
4600 IF ST<8192 OR ST>16383 THEN ER=5:GOSUB 4070:GOTO 4590
4610 OPEN FL$+".MCR" FOR INPUT AS #2 LEN=4:INPUT#2,A$:INPUT#2,A$:FOR I=1 TO VAL(RANGE$):INPUT#2,A$:MRY(ST-8193+I)=VAL(A$):NEXT:GOSUB 4190:GOTO 4280
4620 IF ERR=71 THEN ER=14:GOSUB 4070:GOTO 4550 ELSE IF ERR=70 THEN ER=15:GOSUB 4070:GOTO 4550 ELSE IF ERR=53 THEN ER=12:GOSUB 4070:GOTO 4550 ELSE IF ERR=72 THEN ER=9:GOSUB 4070:GOTO 4550
4630 ' ***** Delete *****
4640 A=0:ON ERROR GOTO 4690:GOSUB 4190:LOCATE 4,58:PRINT "Delete":LOCATE 6,50:PRINT "File name   : ________":LOCATE 10,53:PRINT "Enter, <- or Esc":FL$=""
4650 Q=64:W=6:DCR$="_":LNG=8:A$=FL$:GOSUB 4710:FL$=A$:IF E$=CHR$(27) THEN GOSUB 4190:GOTO 4280
4660 FOR I=1 TO EF:GET#1,I:IF X$<>"0" THEN IF FLNM$=FL$+SPACE$(8-LEN(FL$)) THEN 4680
4670 NEXT:FL$="":ER=12:GOSUB 4070:GOTO 4640
4680 RSET X$="0":PUT #1,I:KILL FL$+".MCR":GOSUB 4190:GOTO 4280
4690 IF ERR=53 THEN ER=12:GOSUB 4070:GOTO 4640 ELSE IF ERR=70 THEN ER=15:GOSUB 4070:GOTO 4640 ELSE IF ERR=71 THEN ER=14:GOSUB 4070:GOTO 4640 ELSE IF ERR=72 THEN ER=9:GOSUB 4070:GOTO 4640
4700 ' ***** Get field *****
4710 COLOR 11,0:LOCATE W,Q:PRINT STRING$(LNG,DCR$):XS=Q:Q=Q+LEN(A$):LOCATE W,XS:PRINT A$:COLOR 3,0
4720 E$=INPUT$(1):IF E$=" " OR E$="." THEN 4720 ELSE IF E$=CHR$(13) THEN IF A=1 THEN RETURN ELSE IF LEN(A$)=0 THEN SOUND 600,BP:GOTO 4720 ELSE LOCATE W,XS:PRINT A$;SPC(LNG-LEN(A$)):RETURN
4730 IF E$=CHR$(27) THEN LOCATE W,XS:PRINT A$;SPC(LNG-LEN(A$)):RETURN
4740 IF E$=CHR$(8) THEN IF LEN(A$)=0 THEN SOUND 600,BP:GOTO 4720 ELSE A$=LEFT$(A$,LEN(A$)-1):Q=Q-1:COLOR 11:LOCATE W,Q:PRINT DCR$:COLOR 3:GOTO 4720
4750 IF LEN(A$)=LNG THEN SOUND 600,BP:GOTO 4720 ELSE Q=Q+1:A$=A$+E$:COLOR 11,0:LOCATE W,XS:PRINT A$:COLOR 3,0:GOTO 4720
4760 ' ***** Load data into dimentions *****
4770 RESTORE 4800:LOCATE 9,23,0:PRINT "…";STRING$(32,205);"ª":LOCATE 10,23:PRINT "∫ çâêÖöê èãÉíé ,öÖêåÅëÅ òÜíÑå Äê ∫":LOCATE 11,23:PRINT "»";STRING$(32,205);"º":FOR I=0 TO 255:READ A$,B$,C$:MEM$(I)=A$+SPACE$(4-LEN(A$))+B$+SPACE$(7-LEN(B$))+C$:NEXT
4780 FOR I=0 TO 15:READ BAN$(I):NEXT:FOR I=1 TO 11:READ CMD$(I):NEXT:FOR I=1 TO 15:READ ER$(I):NEXT:FOR I=0 TO 15:READ OT$(I):NEXT:FOR I=0 TO 15:READ FL$(I):NEXT:FOR I=0 TO 15:READ P(I):NEXT
4790 FOR I=1 TO 8:READ Y(I):NEXT:FOR J=1 TO 6:FOR I=1 TO 8:READ X(J,I):NEXT I,J:RETURN
4800 DATA NOP,,300161,LXI,"B,$$$$",310243,STAX,B,410401,INX,B,310561,INR,B,312031,DCR,B,312171,MVI,"B,$$",312212,RLC,0
4805 DATA 302371,HOLD,$$,402532,DAD,B,312061,LDAX,B,411431,DCX,B,311361,INR,C,311371,DCR,C,311381,MVI,"C,$$",311392,RRC,,301401,,,001410,LXI
4810 DATA "D,$$$$",311423,STAX,D,411351,INX,D,311281,INR,D,311291,DCR,D,311301,MVI,"D,$$",311312,RAL,,301321,,,001330
4815 DATA DAD,D,311341,LDAX,D,411981,DCX,D,311671,INR,E,311601,DCR,E,311611,MVI,"E,$$",311622,RAR,,301631,RIM,,301641,LXI,"H,$$$$",311653
4820 DATA SHLD,$$$$,401663,INX,H,312301,INR,H,312051,DCR,H,312201,MVI,"H,$$",312522,DAA,,300471,,,000630,DAD,H,311911
4825 DATA LHLD,$$$$,401843,DCX,H,311851,INR,L,311861,DCR,L,311871,MVI,"L,$$",311882,CMA,,301891,SIM,,301901,LXI,"SP,$$$$",322123,STA,$$$$
4830 DATA 301963,INX,SP,322441,INR,M,312361,DCR,M,312541,MVI,"M,$$",312282,STC,,302041,,,000390,DAD,SP,320091,LDA,$$$$
4835 DATA 300253,DCX,SP,320411,INR,A,310571,DCR,A,310611,MVI,"A,$$",310052,CMC,,300131,MOV,"B,B",330211,MOV,"B,C",330291,MOV,"B,D",330371
4840 DATA MOV,"B,E",330451,MOV,"B,H",330531,MOV,"B,L",330111,MOV,"B,M",330271,MOV,"B,A",330431,MOV,"C,B",330591,MOV
4845 DATA "C,C",332431,MOV,"C,D",332511,MOV,"C,E",331181,MOV,"C,H",330081,MOV,"C,L",332191,MOV,"C,M",330601,MOV,"C,A",330041,MOV,"D,B",330121,MOV
4850 DATA "D,C",330201,MOV,"D,D",330281,MOV,"D,E",330361,MOV,"D,H",330441,MOV,"D,L",330521,MOV,"D,M",330031,MOV,"D,A"
4855 DATA 330191,MOV,"E,B",330351,MOV,"E,C",330511,MOV,"E,D",332181,MOV,"E,E",332501,MOV,"E,H",331951,MOV,"E,L",332101,MOV,"E,M",331941,MOV,"E,A"
4860 DATA 332421,MOV,"H,B",332341,MOV,"H,C",332261,MOV,"H,D",332021,MOV,"H,E",330581,MOV,"H,H",330101,MOV,"H,L",330261
4865 DATA MOV,"H,M",330421,MOV,"H,A",330011,MOV,"L,B",330171,MOV,"L,C",330331,MOV,"L,D",330491,MOV,"L,E",331271,MOV,"L,H",331201,MOV,"L,L"
4870 DATA 331211,MOV,"L,M",331221,MOV,"L,A",331231,MOV,"M,B",331241,MOV,"M,C",331251,MOV,"M,D",331261,MOV,"M,E",330711
4875 DATA MOV,"M,H",330641,MOV,"M,L",330651,HLT,,300661,MOV,"M,A",330671,MOV,"A,B",330681,MOV,"A,C",330691,MOV,"A,D",330701,MOV,"A,E",330791,MOV
4880 DATA "A,H",330721,MOV,"A,L",330731,MOV,"A,M",330741,MOV,"A,A",330751,ADD,B,310761,ADD,C,310771,ADD,D,310781,ADD,E
4885 DATA 310871,ADD,H,310801,ADD,L,310811,ADD,M,310821,ADD,A,310831,ADC,B,310841,ADC,C,310851,ADC,D,310861,ADC,E,310951,ADC,H,310881,ADC,L
4890 DATA 310891,ADC,M,310901,ADC,A,310911,SUB,B,310921,SUB,C,310931,SUB,D,310941,SUB,E,311031,SUB,H,310961,SUB,L,310971
4895 DATA SUB,M,310981,SUB,A,310991,SBB,B,311001,SBB,C,311011,SBB,D,311021,SBB,E,311111,SBB,H,311041,SBB,L,311051,SBB,M,311061,SBB,A,311071
4900 DATA ANA,B,311081,ANA,C,311091,ANA,D,311101,ANA,E,311191,ANA,H,311121,ANA,L,311131,ANA,M,311141,ANA,A,311151,XRA,B
4905 DATA 311161,XRA,C,311171,XRA,D,310621,XRA,E,310061,XRA,H,310141,XRA,L,310221,XRA,M,310301,XRA,A,310381,ORA,B,310461,ORA,C,310541,ORA,D
4910 DATA 310001,ORA,E,311831,ORA,H,311761,ORA,L,311771,ORA,M,311781,ORA,A,311791,CMP,B,311801,CMP,C,311811,CMP,D,311821
4915 DATA CMP,E,312461,CMP,H,312111,CMP,L,312331,CMP,M,311931,CMP,A,312091,RNZ,,302251,POP,B,312411,JNZ,$$$$,301973,JMP,$$$$,302133,CNZ,$$$$
4920 DATA 302293,PUSH,B,412451,ADI,$$,300232,RST,0,310311,RZ,,202161,RET,,302011,JZ,$$$$,200323,,,000070,CZ,$$$$,202483
4925 DATA CALL,$$$$,402083,ACI,$$,301922,RST,1,312401,RNC,,302321,POP,D,312241,JNC,$$$$,300153,OUT,#$$,311992,CNC,$$$$,302073,PUSH,D,412151
4930 DATA SUI,$$,302232,RST,2,312311,RC,,202391,,,002470,JC,$$$$,202553,IN,#$$,212002,CC,$$$$,201593,,,001520,SBI,$$
4935 DATA 301532,RST,3,311543,RPO,,301551,POP,H,311561,JPO,$$$$,301573,XTHL,,401581,CPO,$$$$,302223,PUSH,H,410341,ANI,$$,300482,RST,4
4940 DATA 312491,RPE,,300501,PCHL,,400021,JPE,$$$$,300183,XCHG,,400551,CPE,$$$$,301513,,,001440,XRI,$$,301452,RST,5
4945 DATA 311461,RP,,201471,POP,PSW,331481,JP,$$$$,201493,DI,,201501,CP,$$$$,202143,PUSH,PSW,432351,ORI,$$,301752,RST,6,311681,RM,,201691,SPHL,0
4950 DATA 401701,JM,$$$$,201713,EI,,201721,CM,$$$$,201733,,,001740,CPI,$$,302382,RST,7,312271,0000,0001,0010,0011,0100
4955 DATA 0101,0110,0111,1000,1001,1010,1011,1100,1101,1110,1111,LS,0000,0000,0000,0000-0000,0000-0000,0000,0000-0000 to 0000,0000
4960 DATA "0000-0000 00 ,00",0000-0000,1  List type mistake,2  Invalid mnemonics operations,3  Invalid mnemonics operand
4965 DATA 4  Undefined hebrew,5  Line out of range,6  Unknowed command,7  Op code syntax error,8  Unkonwn op code,9 Disk media error
4970 DATA 10 Syntax error,11 Printer not ready,12 File not found,13 Disk full,14 Disk not ready,15 Disk write protected
4975 DATA "˘∫˘∫˘∫˘","˘∫˘∫˘∫˛","˘∫˘∫˛∫˘","˘∫˘∫˛∫˛","˘∫˛∫˘∫˘","˘∫˛∫˘∫˛","˘∫˛∫˛∫˘","˘∫˛∫˛∫˛","˛∫˘∫˘∫˘","˛∫˘∫˘∫˛","˛∫˘∫˛∫˘","˛∫˘∫˛∫˛","˛∫˛∫˘∫˘","˛∫˛∫˘∫˛","˛∫˛∫˛∫˘","˛∫˛∫˛∫˛"
4980 DATA "s z p c","s z p C","s z P c","s z P C","s Z p c","s Z p C","s Z P c","s Z P C","S z p c","S z p C","S z P c","S z P C","S Z p c","S Z p C"
4985 DATA "S Z P c","S Z P C",0,1,1,0,1,1,0,1,1,0,0,1,0,1,1,0,22,21,19,18,22,20,19,21,56,57,57,56,58,56,55,55,60,61,61,60,62,60,59,59,64,65,65,64,66,64
4990 DATA 63,63,68,69,69,68,70,68,67,67,73,74,74,73,75,73,72,72,77,78,78,77,79,77,76,76
